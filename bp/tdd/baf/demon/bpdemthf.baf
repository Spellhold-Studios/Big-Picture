IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  OR(2)
    Heard([NEUTRAL],79)
    Heard([EVILCUTOFF],79)
  Allegiance(Myself,NEUTRAL)
  !Global("BP_PlotMan","LOCALS",1)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  GlobalTimerNotExpired("BPCHASE","LOCALS")
  Global("BPCHASER","LOCALS",1)
  OR(2)
    See([GOODCUTOFF])
    See(LastHitter(Myself))
  OR(2)
    ModalState(STEALTH)
    StateCheck(Myself,STATE_INVISIBLE)
THEN
  RESPONSE #100
    SetGlobal("BPCHASER","LOCALS",0)
    AttackReevaluate(LastSeenBy(Myself),135)
END

IF
  GlobalTimerNotExpired("BPCHASE","LOCALS")
  Global("BPCHASER","LOCALS",1)
  OR(2)
    See([GOODCUTOFF])
    See(LastHitter(Myself))
THEN
  RESPONSE #50
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    AttackReevaluate(LastSeenBy(Myself),15)
  RESPONSE #30
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    SetGlobalTimer("RANG","LOCALS",9)
    SetGlobalTimer("DemTel","LOCALS",9)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #20
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(Myself,TELEKINESIS)
    SetGlobal("BP_BehTel","LOCALS",1)
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  GlobalTimerExpired("BPCHASE","LOCALS")
  Global("BPCHASER","LOCALS",1)
  !See([GOODCUTOFF])
  !See(LastHitter(Myself))
THEN
  RESPONSE #70
    DisplayStringHead(Myself,@1)
    SetGlobal("BPCHASER","LOCALS",0)
    ClearActions(Myself)
    Continue()
  RESPONSE #30
    DisplayStringHead(Myself,@2)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(LastHitter(Myself),WIZARD_DIMENSION_DOOR)
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,PC)
  OR(2)
    AttackedBy([GOODCUTOFF],DEFAULT)
    Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Enemy()
    Shout(151)
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,GOODCUTOFF)
  OR(2)
    HitBy([GOODCUTOFF],CRUSHING)
    Allegiance(LastHitter(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Shout(151)
    Enemy()
END

IF
  OR(4)
    CheckStat(Myself,11,WIZARD_GREATER_MALISON)
    CheckStat(Myself,16,WIZARD_GREATER_MALISON)
    CheckStat(Myself,17,WIZARD_GREATER_MALISON)
    CheckStat(Myself,20,WIZARD_GREATER_MALISON)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@3)
    SpellNoDec(Myself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF
  TookDamage()
  Global("BPCHASER","LOCALS",0)
  !See(LastHitter(Myself))
  !See([GOODCUTOFF])
THEN
  RESPONSE #35
    DisplayStringHead(Myself,@4)
    SetGlobalTimer("BPCHASE","LOCALS",12)
    SetGlobal("BPCHASER","LOCALS",1)
    MoveToObject(LastHitter(Myself))
  RESPONSE #30
    DisplayStringHead(Myself,@4)
    SetGlobalTimer("BPCHASE","LOCALS",12)
    SetGlobal("BPCHASER","LOCALS",1)
    Hide()
    MoveToObject(LastHitter(Myself))
  RESPONSE #20
    DisplayStringHead(Myself,@4)
    RunAwayFrom(LastHitter(Myself),45)
  RESPONSE #15
    DisplayStringHead(Myself,@2)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(LastHitter(Myself),WIZARD_DIMENSION_DOOR)
END

IF
  CheckStatGT(Myself,10,WIZARD_GREATER_MALISON)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  CheckStatLT(LastSeenBy(Myself),11,WIZARD_GREATER_MALISON)
  CheckStatLT(TenthNearest,11,WIZARD_GREATER_MALISON)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #35
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #65
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@6)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    Wait(1)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_STUN)
    Wait(1)
    ReallyForceSpell(TenthNearest,WIZARD_DIMENSION_DOOR)
END

IF
  CheckStatGT(Myself,10,WIZARD_GREATER_MALISON)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  CheckStatLT(LastSeenBy(Myself),11,WIZARD_GREATER_MALISON)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  CheckStatGT(Myself,10,WIZARD_GREATER_MALISON)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(TenthNearest([GOODCUTOFF]))
    See(TenthNearest([PC]))
  CheckStatLT(LastSeenBy(Myself),11,WIZARD_GREATER_MALISON)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    EquipMostDamagingMelee()
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  CheckStatGT(Myself,10,WIZARD_GREATER_MALISON)
  CheckStatGT(Myself,5,INT)
  See(TenthNearest)
  CheckStatLT(TenthNearest,11,WIZARD_GREATER_MALISON)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(TenthNearest,WIZARD_DIMENSION_DOOR)
END

IF
  CheckStatGT(Myself,10,WIZARD_GREATER_MALISON)
  CheckStatGT(Myself,5,INT)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
  !See([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("BPCHASER","LOCALS",1)
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(Player1,WIZARD_DIMENSION_DOOR)
END

IF
  !Allegiance(Myself,EVILCUTOFF)
THEN
  RESPONSE #100
END

IF
  TargetUnreachable([GOODCUTOFF])
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  !GlobalTimerNotExpired("BPTS","LOCALS")
  Global("BPWatch1","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("BPWatch1","LOCALS",0)
    SmallWait(6)
    ClearActions(Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("BPRW","LOCALS")
  Global("BPWatch","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("BPWatch","LOCALS",0)
    SmallWait(6)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("BPT","LOCALS")
  !Detect([GOODCUTOFF])
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  RandomNumLT(10,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Hide()
END

IF
  HaveSpell(NPC_SET_BOMB_TRAP)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  RandomNumLT(12,7)
THEN
  RESPONSE #70
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    CreateCreatureObjectOffset("SNARE4",Myself,[-5.-5])
    RemoveSpell(NPC_SET_BOMB_TRAP)
    RandomWalkContinuous()
  RESPONSE #30
    RemoveSpell(NPC_SET_BOMB_TRAP)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_TIME_TRAP)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  RandomNumLT(14,8)
THEN
  RESPONSE #70
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    ApplySpell(Myself,NPC_SET_TIME_TRAP)
    SmallWait(10)
    RemoveSpell(NPC_SET_TIME_TRAP)
    RandomWalkContinuous()
  RESPONSE #30
    RemoveSpell(NPC_SET_TIME_TRAP)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_SPIKE_TRAP)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  RandomNumLT(10,6)
THEN
  RESPONSE #70
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    CreateCreatureObjectOffset("SNARE3",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SPIKE_TRAP)
    RandomWalkContinuous()
  RESPONSE #30
    RemoveSpell(NPC_SET_SPIKE_TRAP)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_SPECIAL_SNARE)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  LevelLT(Myself,21)
  RandomNumLT(7,5)
THEN
  RESPONSE #60
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    CreateCreatureObjectOffset("SNARE2B",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SPECIAL_SNARE)
    RandomWalkContinuous()
  RESPONSE #40
    RemoveSpell(NPC_SET_SPECIAL_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_SPECIAL_SNARE)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  LevelGT(Myself,20)
  RandomNumLT(7,5)
THEN
  RESPONSE #70
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    CreateCreatureObjectOffset("SNARE2C",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SPECIAL_SNARE)
    RandomWalkContinuous()
  RESPONSE #30
    RemoveSpell(NPC_SET_SPECIAL_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  LevelLT(Myself,21)
  RandomNumLT(8,5)
THEN
  RESPONSE #60
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",12)
    CreateCreatureObjectOffset("SNARE1B",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #40
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  LevelGT(Myself,20)
  RandomNumLT(8,5)
THEN
  RESPONSE #70
    SetGlobalTimer("BPTS","LOCALS",12)
    SetGlobal("BPWatch1","LOCALS",1)
    CreateCreatureObjectOffset("SNARE1C",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #30
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    RandomWalkContinuous()
END

IF
  StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerNotExpired("BPRW","LOCALS")
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("BPWatch","LOCALS",1)
    SetGlobalTimer("BPRW","LOCALS",3)
    RandomWalkContinuous()
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  Detect([GOODCUTOFF])
  Exists(LastAttackerOf(Myself))
  OR(8)
    !See([GOODCUTOFF])
    !See(LastAttackerOf(Myself))
    StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    StateCheck(NearestEnemyOf(Myself),STATE_ILLUSIONS)
    StateCheck(NearestEnemyOf(Myself),STATE_BLUR)
    Gender(NearestEnemyOf(Myself),ILLUSIONARY)
    Specifics(NearestEnemyOf(Myself),SIMULACRUM)
    Specifics(NearestEnemyOf(Myself),NEEDS_BREACH)
  RandomNumLT(99,56)
THEN
  RESPONSE #80
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@7)
    DisplayString(Myself,31858)  // ~Detects Illusion~
    ReallyForceSpellRES("SPIN503",Myself)
  RESPONSE #20
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,31859)  // ~Fails to detect illusions~
END

IF
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  Global("BP_BehTel","LOCALS",0)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    ForceSpell(Myself,TELEKINESIS)
    SetGlobal("BP_BehTel","LOCALS",1)
END

IF
  ActionListEmpty()
  SpellCastPriest(Myself,0)
  HaveAnySpells()
  See(SecondNearest([EVILCUTOFF]))
  !GlobalTimerNotExpired("SBN","LOCALS")
  OR(2)
    Range(SecondNearestEnemyOf(Myself),8)
    Range(NearestEnemyOf(Myself),5)
THEN
  RESPONSE #100
    SetGlobalTimer("SBN","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,64573)  // ~Help! Guards, protect me!~
    Shout(69)
END

IF
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),2)
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,CLERIC_BLADE_BARRIER)
    CheckStatGT(NearestEnemyOf(Myself),0,WIZARD_FIRE_SHIELD)
THEN
  RESPONSE #80
    DisplayStringHead(Myself,314)  // ~Repulsion field~
    RunAwayFrom(NearestEnemyOf(Myself),4)
    ReallyForceSpell(NearestEnemyOf(Myself),DEMON_REPULSION)
  RESPONSE #20
    Continue()
END

IF
  TargetUnreachable(LastSeenBy(Myself))
  Delay(3)
THEN
  RESPONSE #100
    ClearActions(Myself)
    Continue()
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("POT","LOCALS")
  HasItem("POTN55",Myself)  // ~Potion of Superior Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN55",Myself)  // ~Potion of Superior Healing~
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("POT","LOCALS")
  HasItem("POTN52",Myself)  // ~Potion of Extra Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN52",Myself)  // ~Potion of Extra Healing~
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  !GlobalTimerNotExpired("BPW","LOCALS")
  !HasItem("POTN52",Myself)  // ~Potion of Extra Healing~
  !HasItem("POTN55",Myself)  // ~Potion of Superior Healing~
  See([EVILCUTOFF])
THEN
  RESPONSE #30
    SetGlobalTimer("BPW","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,72808)  // ~I need healing!~
    Shout(123)
  RESPONSE #70
    Continue()
END

IF
  ActionListEmpty()
  HasItem("POTN10",Myself)  // ~Potion of Invisibility~
  !GlobalTimerNotExpired("BPT","LOCALS")
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN10",Myself)  // ~Potion of Invisibility~
END

IF
  !GlobalTimerNotExpired("DemTel","LOCALS")
  RandomNumLT(5,4)
  OR(5)
    See([PC.0.0.BARD_ALL])
    See([PC.0.0.DRUID_ALL])
    See([PC.0.0.SORCERER])
    See([PC.0.0.LONG_BOW])
    See([PC.0.0.CLERIC_ALL])
  !Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #90
    SetGlobalTimer("DemTel","LOCALS",15)
    DisplayStringHead(Myself,@8)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #40
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  HaveSpell(ROGUE_GREATER_EVASION)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !GlobalTimerNotExpired("EVA","LOCALS")
  RandomNumLT(8,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,63993)  // ~Greater Evasion~
    Spell(Myself,ROGUE_GREATER_EVASION)
    SetGlobalTimer("EVA","LOCALS",FIVE_ROUNDS)
END

IF
  ActionListEmpty()
  HaveSpell(ROGUE_EVASION)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !GlobalTimerNotExpired("EVA","LOCALS")
  RandomNumLT(9,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,63992)  // ~Evasion~
    Spell(Myself,ROGUE_EVASION)
    SetGlobalTimer("EVA","LOCALS",THREE_ROUNDS)
END

IF
  ActionListEmpty()
  HaveSpell(ROGUE_AVOID_DEATH)
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("DW","LOCALS")
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(10,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,64001)  // ~Avoid Death~
    Spell(Myself,ROGUE_AVOID_DEATH)
    SetGlobalTimer("DW","LOCALS",FIVE_ROUNDS)
END

IF
  HitBy([GOODCUTOFF],CRUSHING)
  OR(2)
    !Range(LastHitter(Myself),7)
    !Range(LastAttackerOf(Myself),7)
  OR(2)
    See(LastHitter(Myself))
    See(LastAttackerOf(Myself))
  !TargetUnreachable(LastSeenBy(Myself))
  !GlobalTimerNotExpired("RANG","LOCALS")
THEN
  RESPONSE #90
    DisplayStringHead(Myself,@9)
    SetGlobalTimer("RANG","LOCALS",9)
    SetGlobalTimer("DemTel","LOCALS",9)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #60
    DisplayStringHead(Myself,@10)
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
  RESPONSE #20
    DisplayStringHead(Myself,@11)
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
  RESPONSE #40
    DisplayStringHead(Myself,@12)
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    MoveToObject(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(31,21)
  OR(8)
    See(NearestEnemyOf(Myself))
    See(MostDamagedOf)
    See(WorstAC)
    See(LastTargetedBy(Myself))
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
    See([0.0.0.0.0.0.LAWFUL_GOOD])
    See([0.0.0.PALADIN])
  RandomNumLT(27,21)
  OR(3)
    See([GOODCUTOFF.0.0.0.0.SUMMONED])
    See([GOODCUTOFF.0.0.0.SIMULACRUM])
    See([GOODCUTOFF.0.0.0.0.ILLUSIONARY])
  RandomNumLT(29,24)
  OR(10)
    See([PC.0.0.BARD_ALL])
    See([PC.0.0.DRUID_ALL])
    See([PC.0.0.RANGER_ALL])
    See([PC.0.0.LONG_BOW])
    See([PC.0.0.SORCERER])
    See([PC.0.0.CLERIC_ALL])
    See([PC.0.0.CLERIC_MAGE])
    See([PC.0.0.CLERIC_RANGER])
    See([PC.0.0.PALADIN_ALL])
    See([PC.0.0.CLERIC.0.0.MASK_GOOD])
  RandomNumLT(30,16)
  See(Nearest)
  RandomNumLT(26,14)
  OR(3)
    See([GOODCUTOFF.0.0.RANGER_ALL])
    See([GOODCUTOFF.0.0.PALADIN_ALL])
    See([GOODCUTOFF.0.0.CLERIC.0.0.MASK_GOOD])
  RandomNumLT(28,10)
  OR(3)
    See([GOODCUTOFF.0.0.MONK])
    See([GOODCUTOFF.0.0.THIEF_ALL])
    See([GOODCUTOFF.0.0.BARD_ALL])
  RandomNumLT(24,9)
  OR(3)
    See(StrongestOf)
    See(BestAC)
    See(LeastDamagedOf)
  RandomNumLT(25,9)
  See(WeakestOf)
  RandomNumLT(23,8)
  See(WorstAC)
  RandomNumLT(22,8)
  See(MostDamagedOf)
  RandomNumLT(32,11)
  RandomNumLT(32,11)
  See(TenthNearest([GOODCUTOFF.0.0.PALADIN_ALL]))
  RandomNumLT(34,12)
  See(TenthNearest([GOODCUTOFF.0.0.LONG_BOW]))
  RandomNumLT(35,12)
  See(TenthNearest([GOODCUTOFF.0.0.CLERIC_ALL]))
  RandomNumLT(33,12)
  See(TenthNearest([0.0.0.0.0.0.LAWFUL_GOOD]))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(7)
    General(LastSeenBy(Myself),UNDEAD)
    Race(LastSeenBy(Myself),DEMONIC)
    Race(LastSeenBy(Myself),RAKSHASA)
    Race(LastSeenBy(Myself),IMP)
    Race(LastSeenBy(Myself),MEPHIT)
    Gender(LastSeenBy(Myself),SUMMONED_DEMON)
    CheckStatGT(LastSeenBy(Myself),0,PROTECTION_FROM_EVIL)
THEN
  RESPONSE #100
END

IF
  ActionListEmpty()
  StateCheck(Myself,STATE_INVISIBLE)
  HPPercentGT(Myself,25)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    MoveToObject(LastSeenBy(Myself))
    SmallWait(25)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  !GlobalTimerNotExpired("DemTel1","LOCALS")
  RandomNumLT(3,3)
  OR(6)
    Class(LastSeenBy(Myself),FIGHTER_ALL)
    Class(LastSeenBy(Myself),RANGER_ALL)
    Class(LastSeenBy(Myself),PALADIN_ALL)
    Class(LastSeenBy(Myself),THIEF_ALL)
    Class(LastSeenBy(Myself),MONK)
    HPGT(LastSeenBy(Myself),50)
  Range(LastSeenBy(Myself),10)
THEN
  RESPONSE #90
    DisplayStringHead(Myself,314)  // ~Repulsion field~
    SetGlobalTimer("DemTel1","LOCALS",TWO_ROUNDS)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_REPULSION)
  RESPONSE #40
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(20,4)
  !StateCheck(LastSeenBy(Myself),STATE_BLIND)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",3)
    ReallyForceSpell(LastSeenBy(Myself),INNATE_IMPROVED_GLOBE_OF_DARKNESS)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,3)
  RandomNumLT(20,7)
  !StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  CheckStat(LastSeenBy(Myself),0,CLERIC_CHAOTIC_COMMANDS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",4)
    ReallyForceSpell(LastSeenBy(Myself),VAMPIRE_DOMINATION)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,6)
  RandomNumLT(20,9)
  NumCreatureVsPartyLT([ENEMY.UNDEAD],6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ReallyForceSpell(LastSeenBy(Myself),COM_PRIEST_GREATER_UNDEAD)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,8)
  RandomNumLT(20,12)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  CheckStat(LastSeenBy(Myself),0,CLERIC_CHAOTIC_COMMANDS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",3)
    ReallyForceSpell(LastSeenBy(Myself),SPECTATOR_SUGGESTION)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,11)
  RandomNumLT(20,15)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",4)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_FEAR)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  !GlobalTimerNotExpired("BPT","LOCALS")
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
  !Range(LastSeenBy(Myself),15)
  !StateCheck(LastSeenBy(Myself),STATE_SILENCED)
  RandomNumLT(4,4)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_MISCAST_MAGIC)
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStatLT(LastSeenBy(Myself),60,SPELLFAILUREMAGE)
  CheckStatLT(LastSeenBy(Myself),60,SPELLFAILUREPRIEST)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
  RandomNumLT(6,5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  ActionListEmpty()
  HaveSpell(ASSASSIN_POISON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStat(LastSeenBy(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
  CheckStat(Myself,0,SCRIPTINGSTATE4)
  RandomNumLT(6,5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,ASSASSIN_POISON)
    DisplayStringHead(Myself,52056)  // ~Poison Weapon~
    AttackOneRound(LastSeenBy(Myself))
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  HaveSpell(ROGUE_ASSASINATION)
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStat(LastSeenBy(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
  RandomNumLT(7,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,61779)  // ~ Plant a blade in your innards, I will!~ [HVILLM05]
    Spell(Myself,ROGUE_ASSASINATION)
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CALL_LIGHTNING)
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStat(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
  AreaType(OUTDOOR)
  RandomNumLT(7,5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_CALL_LIGHTNING)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CAUSE_SERIOUS_WOUNDS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !HasItem("SERIOUS",Myself)  // ~Cause Serious Wounds~
  CheckStat(LastSeenBy(Myself),0,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
  RandomNumLT(8,5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_CAUSE_SERIOUS_WOUNDS)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  HPPercentLT(Myself,26)
  Range(NearestEnemyOf(Myself),5)
  !GlobalTimerNotExpired("BPT","LOCALS")
  See(TenthNearest([EVILCUTOFF]))
  !Range(LastSeenBy(Myself),15)
THEN
  RESPONSE #90
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
  RESPONSE #10
    Continue()
END

IF
  HPPercentLT(Myself,36)
  !GlobalTimerNotExpired("BPW","LOCALS")
  Detect(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #80
    SetGlobalTimer("BPW","LOCALS",8)
    DisplayStringHead(Myself,49767)  // ~No more~
    Shout(124)
    RunAwayFromNoInterrupt(NearestEnemyOf(Myself),90)
  RESPONSE #15
    SetGlobalTimer("BPW","LOCALS",10)
    DisplayStringHead(Myself,@13)
    Shout(124)
    EquipMostDamagingMelee()
    Attack(LastHitter(Myself))
  RESPONSE #40
    SetGlobalTimer("BPW","LOCALS",FOUR_ROUNDS)
    DisplayStringHead(Myself,314)  // ~Repulsion field~
    ReallyForceSpell(NearestEnemyOf(Myself),DEMON_REPULSION)
    ReallyForceSpell(SecondNearestEnemyOf(Myself),DEMON_REPULSION)
    ReallyForceSpell(ThirdNearestEnemyOf(Myself),DEMON_REPULSION)
  RESPONSE #40
    SetGlobalTimer("BPW","LOCALS",16)
    DisplayStringHead(Myself,49765)  // ~Slay my attacker!~
    Shout(123)
    ReallyForceSpell(LastHitter(Myself),COM_PRIEST_GREATER_UNDEAD)
END

IF
  CheckStatGT(LastSeenBy(Myself),10,WIZARD_GREATER_MALISON)
  CheckStatLT(Myself,11,WIZARD_GREATER_MALISON)
THEN
  RESPONSE #60
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #90
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  OR(3)
    !Range(LastSeenBy(Myself),5)
    CheckStatGT(LastSeenBy(Myself),0,CLERIC_BLADE_BARRIER)
    CheckStatGT(LastSeenBy(Myself),0,WIZARD_FIRE_SHIELD)
THEN
  RESPONSE #60
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #90
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,CLERIC_BLADE_BARRIER)
    CheckStatGT(LastSeenBy(Myself),0,WIZARD_FIRE_SHIELD)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
END

IF
  !Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #90
    EquipMostDamagingMelee()
    AttackReevaluate(NearestEnemyOf(Myself),45)
  RESPONSE #40
    EquipMostDamagingMelee()
    AttackReevaluate(LastSeenBy(Myself),75)
END

IF
  True()
THEN
  RESPONSE #60
    AttackReevaluate(LastSeenBy(Myself),25)
  RESPONSE #60
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
  RESPONSE #45
END

