IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  HPLT(Myself,15)
  GlobalLT("EfreetiGasForm","LOCALS",3)
  !StateCheck(Myself,STATE_DEAD)
THEN
  RESPONSE #100
    IncrementGlobal("EfreetiGasForm","LOCALS",1)
    ReallyForceSpell(Myself,EFREETI_GAS_FORM_CHANGE)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  Global("Shield","LOCALS",0)
  RandomNumLT(4,4)
  !StateCheck(Myself,STATE_INVISIBLE)
  !HasBounceEffects(Myself)
  CheckStat(Myself,0,125)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
    SetGlobal("Shield","LOCALS",1)
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  Global("EfreetiFireball","LOCALS",0)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(5,4)
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    HPPercentLT(Myself,50)
  CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
    SetGlobal("EfreetiFireball","LOCALS",1)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  GlobalLT("EfreetiInvisibility","LOCALS",3)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(6,6)
  !StateCheck(Myself,STATE_BLUR)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !StateCheck(Myself,STATE_NOT_VISIBLE)
  CheckStatLT(Myself,75,SPELLFAILUREMAGE)
  CheckStatLT(NearestEnemyOf,1,SEEINVISIBLE)
  CheckStatLT(SecondNearestEnemyOf,1,SEEINVISIBLE)
  CheckStatLT(ThirdNearestEnemyOf,1,SEEINVISIBLE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    IncrementGlobal("EfreetiInvisibility","LOCALS",1)
    ForceSpell(Myself,WIZARD_INVISIBILITY)
    Wait(1)
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  Global("arrow","LOCALS",0)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(7,5)
  CheckStatLT(LastSeenBy(Myself),51,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  HPGT(LastSeenBy(Myself),15)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ForceSpell(SixthNearestEnemyOf(Myself),WIZARD_FLAME_ARROW)
    SetGlobal("arrow","LOCALS",1)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_GHOST_ARMOR)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(3,3)
  CheckStatGT(Myself,2,ARMORCLASS)
  !CheckStat(Myself,4,SCRIPTINGSTATE5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_GHOST_ARMOR)
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  Global("strike","LOCALS",0)
  RandomNumLT(8,6)
  CheckStatLT(LastSeenBy(Myself),26,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    ForceSpell(LastSeenBy(Myself),CLERIC_FLAME_STRIKE)
    SetGlobal("strike","LOCALS",1)
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  HaveSpell(WIZARD_LIGHTNING_BOLT)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !Range(LastSeenBy(Myself),6)
  LOS(LastSeenBy(Myself),25)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
  CheckStatLT(Myself,75,SPELLFAILUREMAGE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
  RandomNumLT(11,7)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_LIGHTNING_BOLT)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(5,3)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGICFIRE)
  CheckStatLT(LastSeenBy(Myself),51,RESISTFIRE)
  HPGT(LastSeenBy(Myself),10)
  Range(LastSeenBy(Myself),15)
  !Range(LastSeenBy(Myself),4)
  !LOS(Player1,15)
  !LOS(Player2,15)
  !LOS(Player3,15)
  !LOS(Player4,15)
  !LOS(Player5,15)
  !LOS(Player6,15)
  CheckStatLT(Myself,26,SPELLFAILUREMAGE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MELF_ACID_ARROW)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(10,5)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),51,RESISTACID)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(Myself,75,SPELLFAILUREMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  ActionListEmpty()
  See(TenthNearestEnemyOf(Myself))
  HaveSpell(WIZARD_STINKING_CLOUD)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(15,7)
  !Range(LastSeenBy(Myself),12)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  !StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
  !General(LastSeenBy(Myself),UNDEAD)
  CheckStatGT(LastSeenBy(Myself),7,SAVEVSDEATH)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
  CheckStatLT(Myself,76,SPELLFAILUREMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_STINKING_CLOUD)
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  HaveSpell(WIZARD_MAGIC_MISSILE)
  RandomNumLT(20,9)
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE5)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(Myself,76,SPELLFAILUREMAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
  !See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

