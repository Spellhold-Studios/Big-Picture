IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  OR(2)
    Heard([NEUTRAL],79)
    Heard([EVILCUTOFF],79)
  Allegiance(Myself,NEUTRAL)
  !Global("BP_PlotMan","LOCALS",1)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,PC)
  OR(2)
    AttackedBy([GOODCUTOFF],DEFAULT)
    Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Enemy()
    Shout(151)
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,GOODCUTOFF)
  OR(2)
    HitBy([GOODCUTOFF],CRUSHING)
    Allegiance(LastHitter(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Shout(151)
    Enemy()
END

//////////////CLOUDLIKE  EFFECTS//////////////////////

IF
  CheckStat(Myself,11,180) // I'm in a stinking cloud
  CheckStatGT(Myself,5,INT)
  !StateCheck(Myself,STATE_SLEEPING) // not yet affected
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(2)
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStatGT(Myself,11,180) // I'm in a web,entangle, or grease spell
  CheckStatLT(Myself,15,180)
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,1,131) // I don't have free action
  CheckStatLT(Myself,1,WEB) //and not webbed up
  CheckStatLT(Myself,1,ENTANGLE) //nor entangled --I can move
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,15,180) // I'm in an Ice Storm
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTCOLD) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,16,180) // I'm in a Cloudkill or Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTPOISON) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,17,180) // I'm in a Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTACID) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,18,180) // Incendiary Cloud, Meteor Swarm or Fire Storm -- bad stuff
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTFIRE) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

// This is to try and keep them from going back in to whomp somebody
IF
  CheckStatLT(Myself,11,180) // No longer in the cloud effects
  CheckStatGT(Myself,5,INT)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  !Range(LastSeenBy(Myself),16)
  Exists([EVILCUTOFF])
  !Range(NearestEnemyOf(Myself),2)
THEN
  RESPONSE #100
    EquipRanged()  //use your bow, then
    SmallWait(3)
    Continue()
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////


IF
  OR(2)
    !Allegiance(Myself,EVILCUTOFF)
    !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  TargetUnreachable(LastSeenBy(Myself))
  Delay(3)
THEN
  RESPONSE #100
    ClearActions(Myself)
    Continue()
END

IF
  Range(NearestEnemyOf(Myself),2)
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(NearestEnemyOf(Myself),0,125)
THEN
  RESPONSE #70
    RunAwayFrom(NearestEnemyOf(Myself),4)
  RESPONSE #30
    Continue()
END

IF
  General(Myself,UNDEAD)
  OR(2)
    TurnedBy([GOODCUTOFF.0.0.CLERIC_ALL])
    TurnedBy([GOODCUTOFF.0.0.PALADIN_ALL])
THEN
  RESPONSE #100
END

IF
  HasItem("potn55",Myself)  // ~Potion of Superior Healing~
  !GlobalTimerNotExpired("POT","LOCALS")
  HPPercentLT(Myself,60)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    UseItem("potn55",Myself)  // ~Potion of Superior Healing~
END

IF
  HasItem("potn52",Myself)  // ~Potion of Extra Healing~
  !GlobalTimerNotExpired("POT","LOCALS")
  HPPercentLT(Myself,70)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    UseItem("potn52",Myself)  // ~Potion of Extra Healing~
END

IF
  HasItem("POTN08",Myself)  // ~Potion of Healing~
  !GlobalTimerNotExpired("POT","LOCALS")
  RandomNumLT(201,80)
  HPPercentLT(Myself,85)
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    UseItem("POTN08",Myself)  // ~Potion of Healing~
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  CheckStatGT(Myself,7,INT)
  OR(2)
    General(Myself,HUMANOID)
    General(Myself,GIANTHUMANOID)
  !HasItem("POTN55",Myself)
  !HasItem("POTN52",Myself)
  !GlobalTimerNotExpired("BPW","LOCALS")
  Range([EVILCUTOFF],15)
THEN
  RESPONSE #80
    SetGlobalTimer("BPW","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,72808)
    Shout(123)
  RESPONSE #20
    Continue()
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  CheckStatGT(Myself,7,INT)
  !General(Myself,HUMANOID)
  !General(Myself,GIANTHUMANOID)
  !HasItem("POTN55",Myself)
  !HasItem("POTN52",Myself)
  !GlobalTimerNotExpired("BPW","LOCALS")
  Range([EVILCUTOFF],15)
THEN
  RESPONSE #80
    SetGlobalTimer("BPW","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@0)
    Shout(123)
  RESPONSE #20
    Continue()
END

IF
  StateCheck(Myself,STATE_POISONED)
  HasItem("potn17",Myself)  // ~Elixir of Health~
  !GlobalTimerNotExpired("POT","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn17",Myself)  // ~Elixir of Health~
END

IF
  StateCheck(Myself,STATE_POISONED)
  HasItem("potn20",Myself)  // ~Antidote~
  !GlobalTimerNotExpired("POT","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn20",Myself)  // ~Antidote~
END

IF
  ActionListEmpty()
  StateCheck(Myself,STATE_POISONED)
  CheckStatGT(Myself,7,INT)
  OR(2)
    General(Myself,HUMANOID)
    General(Myself,GIANTHUMANOID)
  Range([EVILCUTOFF],15)
  !GlobalTimerNotExpired("POIS","LOCALS")
  !HasItem("POTN17",Myself)  // ~Elixir of Health~
  !HasItem("POTN20",Myself)  // ~Antidote~
THEN
  RESPONSE #100
    Shout(83)
    DisplayStringHead(Myself,@1)
    SetGlobalTimer("POIS","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  StateCheck(Myself,STATE_POISONED)
  CheckStatGT(Myself,7,INT)
  !General(Myself,HUMANOID)
  !General(Myself,GIANTHUMANOID)
  Range([EVILCUTOFF],15)
  !GlobalTimerNotExpired("POIS","LOCALS")
  !HasItem("POTN17",Myself)  // ~Elixir of Health~
  !HasItem("POTN20",Myself)  // ~Antidote~
THEN
  RESPONSE #100
    Shout(83)
    DisplayStringHead(Myself,@2)
    SetGlobalTimer("POIS","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  HasItem("SCRL1V",Myself)  // ~Stoneskin~
  !GlobalTimerNotExpired("BPT","LOCALS")
  CheckStatGT(Myself,10,INT)
  CheckStatLT(Myself,1,STONESKINS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1V",Myself)  // ~Stoneskin~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL07",Myself)  // ~Protection from Magic~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNum(3,3)
  !HaveAnySpells()
  CheckStatGT(Myself,10,INT)
  OR(5)
    See([GOODCUTOFF.0.0.CLERIC_ALL])
    See([GOODCUTOFF.0.0.BARD_ALL])
    See([GOODCUTOFF.0.0.DRUID_ALL])
    See([GOODCUTOFF.0.0.LONG_BOW])
    See([GOODCUTOFF.0.0.SORCERER])
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
    UseItem("SCRL07",Myself)  // ~Protection from Magic~
END

IF
  ActionListEmpty()
  HasItem("SCRL1H",Myself)  // ~Haste~
  !GlobalTimerNotExpired("BPT","LOCALS")
   RandomNumLT(5,3)
 CheckStatGT(Myself,10,INT)
  !StateCheck(Myself,STATE_HASTED)
  CheckStat(Myself,0,IMPROVEDHASTE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1H",Myself)  // ~Haste~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1N",Myself)  // ~Protection From Normal Missiles~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNum(4,1)
  CheckStatGT(Myself,10,INT)
  CheckStat(Myself,0,179)
  OR(3)
    !Range(NearestEnemyOf(Myself),6)
    !Range(LastAttackerOf(Myself),3)
    AttackedBy([ANYONE],RANGED)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1N",Myself)  // ~Protection From Normal Missiles~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1J",Myself)  // ~Invisibility 10' Radius~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(7,3)
  CheckStatGT(Myself,10,INT)
  Range(SecondNearestMyGroupOfType([EVILCUTOFF]),10)
  !StateCheck(Myself,0x60400010)
  CheckStatLT(NearestEnemyOf,1,SEEINVISIBLE)
  CheckStatLT(SecondNearestEnemyOf,1,SEEINVISIBLE)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1J",Myself)  // ~Invisibility 10' Radius~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStatLT(Myself,15,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,6,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStat(Myself,15,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,5,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStat(Myself,16,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,4,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStat(Myself,17,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,3,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStatGT(Myself,17,DEX)
  CheckStatLT(Myself,21,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,2,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL67",Myself)  // ~Armor~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,3)
  CheckStatGT(Myself,10,INT)
  CheckStatGT(Myself,20,DEX)
  CheckStatLT(Myself,3,SCRIPTINGSTATE5)
  CheckStatGT(Myself,1,ARMORCLASS)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL67",Myself)  // ~Armor~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HitBy([GOODCUTOFF],CRUSHING)
  CheckStatGT(Myself,7,INT)
  OR(2)
    General(Myself,HUMANOID)
    General(Myself,GIANTHUMANOID)
  !GlobalTimerNotExpired("RANG","LOCALS")
  !StateCheck(Myself,STATE_POISONED)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  !Range(LastSeenBy(Myself),7)
  !TargetUnreachable(LastSeenBy(Myself))
  !InWeaponRange(LastSeenBy(Myself))
  HPGT(LastSeenBy(Myself),0)
THEN
  RESPONSE #50
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@3)
  RESPONSE #50
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@4)
  RESPONSE #25
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@5)
  RESPONSE #25
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@6)
  RESPONSE #50
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@7)
    MoveToObject(LastSeenBy(Myself))
    AttackReevaluate(LastSeenBy(Myself),180)
END

IF
  ActionListEmpty()
  HasItem("SCRL94",Myself)  // ~Resist Fear~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(6,5)
  CheckStatGT(Myself,10,INT)
  OR(3)
    StateCheck([EVILCUTOFF],STATE_PANIC)
    StateCheck(SecondNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(ThirdNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
  Range(ThirdNearestMyGroupOfType([EVILCUTOFF]),15)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL94",Myself)  // ~Resist Fear~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END



IF
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(28,22)
  OR(4)
    See([GOODCUTOFF.0.0.0.0.SUMMONED_DEMON])
    See([GOODCUTOFF.0.0.0.0.SUMMONED])
    See([GOODCUTOFF.0.0.0.SIMULACRUM])
    See([GOODCUTOFF.0.0.0.0.ILLUSIONARY])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(31,26)
  OR(6)
    See(NearestEnemyOf(Myself))
    See(WeakestOf)
    See(WorstAC)
    See(MostDamagedOf)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END


IF
  OR(3)
    Allegiance(LastSeenBy(Myself),ENEMY)
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODBUTRED)
THEN
  RESPONSE #100
END

IF
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
THEN
  RESPONSE #80
    Continue()
  RESPONSE #20
END

IF
  ActionListEmpty()
  StateCheck(Myself,0x00400010)
  Class(Myself,THIEF_ALL)
  HPPercentGT(Myself,25)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",12)
    EquipMostDamagingMelee()
    MoveToObject(LastSeenBy(Myself))
    FollowObjectFormation(LastSeenBy(Myself),1,3)
    SmallWait(5)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  CheckStatGT(Myself,0,SCRIPTINGSTATE4)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END



IF
  RandomNumLT(29,23)
  OR(7)
    See([PC.0.0.RANGER_ALL])
    See([PC.0.0.PALADIN_ALL])
    See([PC.0.0.BARD_ALL])
    See([PC.0.0.DRUID_ALL])
    See([PC.0.0.CLERIC_ALL])
    See([PC.0.0.LONG_BOW])
    See([PC.0.0.SORCERER])
  RandomNumLT(30,16)
  See(Protagonist)
  RandomNumLT(26,10)
  OR(4)
    See([GOODCUTOFF.0.0.RANGER_ALL])
    See([GOODCUTOFF.0.0.DRUID_ALL])
    See([GOODCUTOFF.0.0.PALADIN_ALL])
    See([GOODCUTOFF.0.0.CLERIC_ALL])
  RandomNumLT(27,10)
  OR(3)
    See([GOODCUTOFF.0.0.MONK])
    See([GOODCUTOFF.0.0.BARD_ALL])
    See([GOODCUTOFF.0.0.THIEF_ALL])
  RandomNumLT(32,11)
  OR(2)
    See(TenthNearest([GOODCUTOFF.0.0.LONG_BOW]))
    See(TenthNearest([GOODCUTOFF.0.0.SORCERER]))
  RandomNumLT(20,8)
  See(TenthNearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END



IF
  ActionListEmpty()
  HasItem("wand02",Myself)  // ~Wand of Fear~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(54,20)
  CheckStatGT(Myself,8,INT)
  Range(LastSeenBy(Myself),16)
  LevelLT(LastSeenBy(Myself),11)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  !HasBounceEffects(LastSeenBy(Myself))
  !General(LastSeenBy(Myself),UNDEAD) // possible w/ pc summons
  !Race(LastSeenBy(Myself),DEMONIC) // possible w/ pc summons
  !StateCheck(LastSeenBy(Myself),270381)
  !StateCheck(LastSeenBy(Myself),0x80100000)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,31971)  // ~uses a wand~
    UseItem("wand02",LastSeenBy(Myself))  // ~Wand of Fear~
END

IF
  ActionListEmpty()
  HasItem("SCRL1U",Myself)  // ~Confusion~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(108,65)
  CheckStatGT(Myself,10,INT)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !General(LastSeenBy(Myself),UNDEAD)
  CheckStatGT(LastSeenBy(Myself),10,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1U",LastSeenBy(Myself))  // ~Confusion~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("POTN26",Myself)  // ~Potion of Explosions~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(107,64)
  CheckStatGT(Myself,10,INT)
  OR(2)
    !Range(LastSeenBy(Myself),15)
    CheckStatGT(Myself,74,RESISTFIRE)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,3)
    CheckStatGT(NearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    !Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(ThirdNearestMyGroupOfType)
    Range(ThirdNearestMyGroupOfType,5)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTMAGIC)
  !StateCheck(LastSeenBy(Myself),0x00400010)
  CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN26",LastSeenBy(Myself))  // ~Potion of Explosions~
END

IF
  ActionListEmpty()
  HasItem("SCRL1G",Myself)  // ~Fireball~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(108,64)
  CheckStatGT(Myself,10,INT)
  OR(2)
    !Range(LastSeenBy(Myself),15)
    CheckStatGT(Myself,74,RESISTFIRE)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,3)
    CheckStatGT(NearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    !Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(ThirdNearestMyGroupOfType)
    Range(ThirdNearestMyGroupOfType,5)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTMAGIC)
  !StateCheck(LastSeenBy(Myself),0x00400010)
  CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1G",LastSeenBy(Myself))  // ~Fireball~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1S",Myself)  // ~Dire Charm~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(118,60)
  CheckStatGT(Myself,10,INT)
  !StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  CheckStat(LastSeenBy(Myself),0,129) //spell turn
  CheckStat(LastSeenBy(Myself),0,119) //chaotic command
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1S",LastSeenBy(Myself))  // ~Dire Charm~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1K",Myself)  // ~Lightning Bolt~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(57,29)
  CheckStatGT(Myself,10,INT)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
  CheckStat(LastSeenBy(Myself),0,129)
  LOS(LastSeenBy(Myself),25)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1K",LastSeenBy(Myself))  // ~Lightning Bolt~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_HOLD_PERSON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(119,60)
  General(LastSeenBy(Myself),HUMANOID)
  CheckStatGT(LastSeenBy(Myself),10,SAVEVSSPELL)
  !HasBounceEffects(LastSeenBy(Myself))
  !Range(NearestEnemyOf(Myself),3)
  CheckStatLT(Myself,76,SPELLFAILUREPRIEST)
  CheckStatLT(LastSeenBy(Myself),1,131) //Free Action
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
END

IF
  ActionListEmpty()
  HaveSpell(WIZARD_WEB)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(121,61)
  !Range(LastSeenBy(Myself),15)
  CheckStat(LastSeenBy(Myself),0,WEB)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  !StateCheck(LastSeenBy(Myself),270381)
  !StateCheck(LastSeenBy(Myself),0x80100000)
  !Range(NearestEnemyOf(Myself),3)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,4)
    CheckStatGT(NearestMyGroupOfType,0,131)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,0,131)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),1,131) //Free Action
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(Myself,76,SPELLFAILUREMAGE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_WEB)
END

IF
  ActionListEmpty()
  HasItem("SCRL99",Myself)  // ~Web~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(65,32)
  CheckStat(LastSeenBy(Myself),0,WEB)
  !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  !StateCheck(LastSeenBy(Myself),270381)
  !StateCheck(LastSeenBy(Myself),0x80100000)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,4)
    CheckStatGT(NearestMyGroupOfType,0,131)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,0,131)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),1,131) //Free Action
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL99",LastSeenBy(Myself))  // ~Web~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL66",Myself)  // ~Grease~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(74,28)
  CheckStatGT(Myself,10,INT)
  !Range(LastSeenBy(Myself),15)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,4)
    CheckStatGT(NearestMyGroupOfType,0,131)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,0,131)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  CheckStatLT(LastSeenBy(Myself),1,GREASE)
  CheckStatLT(LastSeenBy(Myself),1,ENTANGLE)
  CheckStatLT(LastSeenBy(Myself),1,HELD)
  CheckStatGT(LastSeenBy(Myself),14,180) //--similar/same effects as grease
  !StateCheck(LastSeenBy(Myself),STATE_SLOWED)    
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),1,131) //Free Action
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL66",LastSeenBy(Myself))  // ~Grease~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1B",Myself)  // ~Agannazar's Scorcher~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(67,27)
  CheckStatGT(Myself,10,INT)
  Range(LastSeenBy(Myself),16)
  CheckStatLT(LastSeenBy(Myself),51,RESISTFIRE)
  CheckStat(LastSeenBy(Myself),0,129)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1B",LastSeenBy(Myself))  // ~Agannazar's Scorcher~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL77",Myself)  // ~Magic Missile~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(70,36)
  CheckStatGT(Myself,10,INT)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE5)
  CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
  CheckStat(LastSeenBy(Myself),0,129)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL77",LastSeenBy(Myself))  // ~Magic Missile~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL83",Myself)  // ~Chromatic Orb~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(63,26)
  CheckStatGT(Myself,10,INT)
  CheckStat(LastSeenBy(Myself),0,129)
  CheckStatGT(LastSeenBy(Myself),10,SAVEVSSPELL)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL83",LastSeenBy(Myself))  // ~Chromatic Orb~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL70",Myself)  // ~Color Spray~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(65,21)
  CheckStatGT(Myself,10,INT)
  Range(LastSeenBy(Myself),5)
  CheckStatLT(LastSeenBy(Myself),8,LEVEL)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  !General(LastSeenBy(Myself),UNDEAD)
  CheckStatGT(LastSeenBy(Myself),10,SAVEVSSPELL)
  OR(3)
    !Exists(NearestMyGroupOfType)
    !Range(NearestMyGroupOfType,5)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(3)
    !Exists(SecondNearestMyGroupOfType)
    !Range(SecondNearestMyGroupOfType,5)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL70",LastSeenBy(Myself))  // ~Color Spray~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL81",Myself)  // ~Sleep~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(70,26)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  CheckStatGT(Myself,10,INT)
  General(LastSeenBy(Myself),HUMANOID)
  !Race(LastSeenBy(Myself),ELF) // BGEE is set up to handle elf sleep resistance
  CheckStatLT(LastSeenBy(Myself),5,LEVEL)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL81",LastSeenBy(Myself))  // ~Sleep~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL68",Myself)  // ~Burning Hands~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(62,24)
  CheckStatGT(Myself,10,INT)
  Range(LastSeenBy(Myself),5)
  CheckStatGT(LastSeenBy(Myself),10,SAVEVSSPELL)
  OR(4)
    !Exists(NearestMyGroupOfType)
    !Range(NearestMyGroupOfType,5)
    CheckStatGT(NearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    !Range(SecondNearestMyGroupOfType,5)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),51,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),76,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL81",LastSeenBy(Myself))  // ~Sleep~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL71",Myself)  // ~Blindness~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(64,22)
  CheckStatGT(Myself,10,INT)
  CheckStat(LastSeenBy(Myself),0,129)
  !StateCheck(LastSeenBy(Myself),STATE_BLIND)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL71",LastSeenBy(Myself))  // ~Blindness~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL84",Myself)  // ~Larloch's Minor Drain~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(130,66)
  CheckStatGT(Myself,10,INT)
  Range(LastSeenBy(Myself),10)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL84",Myself)  // ~Larloch's Minor Drain~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END



IF
  RandomNumLT(24,9)
  OR(3)
    See(StrongestOf)
    See(BestAC)
    See(LeastDamagedOf)
  RandomNumLT(25,9)
  See(WeakestOf)
  RandomNumLT(23,8)
  See(WorstAC)
  RandomNumLT(21,8)
  See(MostDamagedOf)
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(32,25)
  OR(3)
    Heard(ThirdNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(SecondNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(NearestMyGroupOfType([EVILCUTOFF]),124)
  HPGT(LastHeardBy(Myself),1)
  Range(LastHeardBy(Myself),20)
  Exists(LastAttackerOf(LastHeardBy(Myself)))
  !Allegiance(LastAttackerOf(LastHeardBy(Myself)),EVILCUTOFF)
  !StateCheck(LastAttackerOf(LastHeardBy(Myself)),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(LastHeardBy(Myself)),MORNINGSTAR)
    !Class(LastAttackerOf(LastHeardBy(Myself)),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(LastHeardBy(Myself)),65)
  OR(3)  
    !StateCheck(LastAttackerOf(LastHeardBy(Myself)),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(LastHeardBy(Myself)))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(33,13)
  HitBy([0],0x0000)
  !Allegiance(LastHitter(Myself),EVILCUTOFF)
  !StateCheck(LastHitter(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
  OR(3)  
    !StateCheck(LastHitter(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumGT(33,22)
  AttackedBy([0],DEFAULT)
  !Allegiance(LastAttackerOf(Myself),EVILCUTOFF)
  !StateCheck(LastAttackerOf(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(Myself),MORNINGSTAR)
    !Class(LastAttackerOf(Myself),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(Myself),65)
  OR(3)  
    !StateCheck(LastAttackerOf(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END



IF
  ActionListEmpty()
  HasItem("SCRL1Q",Myself)  // ~Vampiric Touch~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(129,65)
  CheckStatGT(Myself,10,INT)
  Range(LastSeenBy(Myself),10)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1Q",LastSeenBy(Myself))  // ~Vampiric Touch~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
END

IF
  ActionListEmpty()
  HasItem("SCRL1C",Myself)  // ~Ghoul Touch~
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(96,32)
  CheckStat(LastSeenBy(Myself),0,181)
  !HasItem("MELFMET",Myself)  // ~Melf's Minute Meteor~
  !HasItem("BLAKBLAD",Myself)  // ~Black Blade of Disaster~
  !HasItem("ENEBLADE",Myself)  // ~Energy Blade~
  !HasItem("PHANBLAD",Myself)  // ~Phantom Blade~
  !HasItem("GHOULT",Myself)  // ~Ghoul Touch~
  Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    UseItem("SCRL1C",Myself)  // ~Ghoul Touch~
    DisplayStringHead(Myself,46153)  // ~uses a scroll~
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("BPW","LOCALS")
  OR(2)
    General(Myself,GIANTHUMANOID)
    General(Myself,HUMANOID)
  CheckStatGT(Myself,7,INT)
  HPPercentLT(Myself,25)
  Range(LastHitter(Myself),10)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
THEN
  RESPONSE #30
    DisplayStringHead(Myself,49767)
    Shout(124)
    SetGlobalTimer("BPW","LOCALS",10)
    RunAwayFrom(LastHitter(Myself),40)
  RESPONSE #30
    DisplayStringHead(Myself,49765)
    Shout(124)
    SetGlobalTimer("BPW","LOCALS",10)
    AttackReevaluate(LastHitter(Myself),15)
  RESPONSE #40
    DisplayStringHead(Myself,49766)
    Shout(123)
    SetGlobalTimer("BPW","LOCALS",10)
    AttackReevaluate(LastSeenBy(Myself),15)
  RESPONSE #25
    DisplayStringHead(Myself,@8)
    SetGlobalTimer("BPW","LOCALS",10)
    Attack(LastHitter(Myself))
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("BPW","LOCALS")
  !General(Myself,GIANTHUMANOID)
  !General(Myself,HUMANOID)
  CheckStatGT(Myself,7,INT)
  HPPercentLT(Myself,25)
  Range(LastHitter(Myself),10)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
THEN
  RESPONSE #30
    DisplayStringHead(Myself,@9)
    Shout(124)
    SetGlobalTimer("BPW","LOCALS",10)
    RunAwayFrom(LastHitter(Myself),40)
  RESPONSE #30
    DisplayStringHead(Myself,@10)
    Shout(124)
    SetGlobalTimer("BPW","LOCALS",10)
    AttackReevaluate(LastHitter(Myself),15)
  RESPONSE #25
    DisplayStringHead(Myself,@11)
    SetGlobalTimer("BPW","LOCALS",10)
    Attack(LastHitter(Myself))
END

IF
  ActionListEmpty()
  CheckStatGT(LastSeenBy(Myself),10,180)
  CheckStatLT(Myself,11,180)
THEN
  RESPONSE #100
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  OR(3)
    !Range(NearestEnemyOf(Myself),5)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #100
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
END

IF
  ActionListEmpty()
  !Range(LastSeenBy(Myself),8)
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
THEN
  RESPONSE #90
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
  RESPONSE #90
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END
