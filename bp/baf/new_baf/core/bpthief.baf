IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  OR(2)
    Heard([NEUTRAL],79)
    Heard([EVILCUTOFF],79)
  Allegiance(Myself,NEUTRAL)
  !Global("BP_PlotMan","LOCALS",1)
THEN
  RESPONSE #100
    Enemy()
    Continue()
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,PC)
  OR(2)
    AttackedBy([GOODCUTOFF],DEFAULT)
    Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Shout(151)
    Enemy()
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  !Allegiance(Myself,GOODCUTOFF)
  OR(2)
    HitBy([GOODCUTOFF],CRUSHING)
    Allegiance(LastHitter(Myself),GOODCUTOFF)
THEN
  RESPONSE #100
    Shout(151)
    Enemy()
    Continue()
END

IF
  !Allegiance(Myself,EVILCUTOFF)
THEN
  RESPONSE #100
END

IF
  TargetUnreachable([GOODCUTOFF])
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  !GlobalTimerNotExpired("BPTS","LOCALS")
  Global("BPWatch1","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("BPWatch1","LOCALS",0)
    SmallWait(6)
    ClearActions(Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("BPRW","LOCALS")
  Global("BPWatch","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("BPWatch","LOCALS",0)
    SmallWait(6)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("BPT","LOCALS")
  !Detect([GOODCUTOFF])
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  RandomNumLT(10,6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    Hide()
END

IF
  ActionListEmpty()
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !Detect([GOODCUTOFF])
  !AreaType(OUTDOOR)
  LevelLT(Myself,11)
  RandomNumLT(8,5)
THEN
  RESPONSE #35
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    CreateCreatureObjectOffset("SNARE1",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #65
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",4)
    RandomWalkContinuous()
END

IF
  ActionListEmpty()
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPTS","LOCALS")
  !Detect([GOODCUTOFF])
  !AreaType(OUTDOOR)
  LevelGT(Myself,10)
  LevelLT(Myself,16)
  RandomNumLT(8,5)
THEN
  RESPONSE #50
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    CreateCreatureObjectOffset("SNARE1A",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #50
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",4)
    RandomWalkContinuous()
END

IF
  ActionListEmpty()
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !See([GOODCUTOFF])
  !AreaType(OUTDOOR)
  LevelGT(Myself,15)
  LevelLT(Myself,21)
  RandomNumLT(8,5)
THEN
  RESPONSE #605
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    CreateCreatureObjectOffset("SNARE1B",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #35
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",4)
    RandomWalkContinuous()
END

IF
  ActionListEmpty()
  HaveSpell(NPC_SET_SNARE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !See([GOODCUTOFF])
  !AreaType(OUTDOOR)
  LevelGT(Myself,20)
  RandomNumLT(8,5)
THEN
  RESPONSE #75
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",6)
    CreateCreatureObjectOffset("SNARE1C",Myself,[-5.-5])
    RemoveSpell(NPC_SET_SNARE)
    RandomWalkContinuous()
  RESPONSE #25
    RemoveSpell(NPC_SET_SNARE)
    SetGlobal("BPWatch1","LOCALS",1)
    SetGlobalTimer("BPTS","LOCALS",4)
    RandomWalkContinuous()
END

IF
  StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerNotExpired("BPRW","LOCALS")
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("BPWatch","LOCALS",1)
    SetGlobalTimer("BPRW","LOCALS",3)
    RandomWalkContinuous()
END

IF
  CheckStatGT(Myself,24,DETECTILLUSIONS)
  CheckStatLT(Myself,40,DETECTILLUSIONS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Detect([GOODCUTOFF])
  Exists(LastAttackerOf(Myself))
  OR(8)
    !See([GOODCUTOFF])
    !See(LastAttackerOf(Myself))
    StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    StateCheck(NearestEnemyOf(Myself),0x60200010)
    StateCheck(NearestEnemyOf(Myself),STATE_BLUR)
    Gender(NearestEnemyOf(Myself),ILLUSIONARY)
    Specifics(NearestEnemyOf(Myself),207)
    Specifics(NearestEnemyOf(Myself),205)
  RandomNumLT(99,56)
THEN
  RESPONSE #35
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,@0)
    DisplayString(Myself,31858)
    ReallyForceSpell(Myself,3503)
  RESPONSE #65
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,31859)
END

IF
  CheckStatGT(Myself,39,DETECTILLUSIONS)
  CheckStatLT(Myself,60,DETECTILLUSIONS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Detect([GOODCUTOFF])
  Exists(LastAttackerOf(Myself))
  OR(6)
    !See([GOODCUTOFF])
    !See(LastAttackerOf(Myself))
    StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    StateCheck(NearestEnemyOf(Myself),0x60200010)
    StateCheck(NearestEnemyOf(Myself),STATE_BLUR)
    Gender(NearestEnemyOf(Myself),ILLUSIONARY)
    Specifics(NearestEnemyOf(Myself),207)
    Specifics(NearestEnemyOf(Myself),205)
  RandomNumLT(99,56)
THEN
  RESPONSE #50
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,@0)
    DisplayString(Myself,31858)
    ReallyForceSpell(Myself,3503)
  RESPONSE #50
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,31859)
END

IF
  CheckStatGT(Myself,59,DETECTILLUSIONS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Detect([GOODCUTOFF])
  Exists(LastAttackerOf(Myself))
  OR(6)
    !See([GOODCUTOFF])
    !See(LastAttackerOf(Myself))
    StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    StateCheck(NearestEnemyOf(Myself),0x60200010)
    StateCheck(NearestEnemyOf(Myself),STATE_BLUR)
    Gender(NearestEnemyOf(Myself),ILLUSIONARY)
    Specifics(NearestEnemyOf(Myself),207)
    Specifics(NearestEnemyOf(Myself),205)
  RandomNumLT(99,56)
THEN
  RESPONSE #70
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,@0)
    DisplayString(Myself,31858)
    ReallyForceSpell(Myself,3503)
  RESPONSE #30
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,31859)
END


//////////////CLOUDLIKE  EFFECTS//////////////////////

IF
  CheckStat(Myself,11,180) // I'm in a stinking cloud
  CheckStatGT(Myself,5,INT)
  !StateCheck(Myself,STATE_SLEEPING) // not yet affected
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(2)
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStatGT(Myself,11,180) // I'm in a web,entangle, or grease spell
  CheckStatLT(Myself,15,180)
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,1,131) // I don't have free action
  CheckStatLT(Myself,1,WEB) //and not webbed up
  CheckStatLT(Myself,1,ENTANGLE) //nor entangled --I can move
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,15,180) // I'm in an Ice Storm
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTCOLD) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,16,180) // I'm in a Cloudkill or Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTPOISON) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,17,180) // I'm in a Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTACID) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,18,180) // Incendiary Cloud, Meteor Swarm or Fire Storm -- bad stuff
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTFIRE) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

// This is to try and keep them from going back in to whomp somebody
IF
  CheckStatLT(Myself,11,180) // No longer in the cloud effects
  CheckStatGT(Myself,5,INT)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  !Range(LastSeenBy(Myself),16)
  Exists([EVILCUTOFF])
  !Range(NearestEnemyOf(Myself),2)
THEN
  RESPONSE #100
    EquipRanged()  //use your bow, then
    SmallWait(3)
    Continue()
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

IF
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  TargetUnreachable(LastSeenBy(Myself))
  Delay(3)
THEN
  RESPONSE #100
    ClearActions(Myself)
    Continue()
END

IF
  Range(NearestEnemyOf(Myself),2)
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(NearestEnemyOf(Myself),0,125)
THEN
  RESPONSE #80
    RunAwayFrom(NearestEnemyOf(Myself),4)
  RESPONSE #20
    Continue()
END

IF
  HasItem("potn52",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  HPPercentLT(Myself,70)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,46150)
    SetGlobalTimer("BPT","LOCALS",6)
    UseItem("potn52",Myself)
END

IF
  HasItem("potn55",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  HPPercentLT(Myself,60)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,46150)
    SetGlobalTimer("BPT","LOCALS",6)
    UseItem("potn55",Myself)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  !GlobalTimerNotExpired("BPW","LOCALS")
  !HasItem("POTN52",Myself)
  !HasItem("POTN55",Myself)
  See([EVILCUTOFF])
THEN
  RESPONSE #30
    SetGlobalTimer("BPW","LOCALS",12)
    Shout(123)
  RESPONSE #70
    Continue()
END

IF
  HasItem("POTN17",Myself)
  StateCheck(Myself,STATE_POISONED)
  !GlobalTimerNotExpired("POT","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",6)
    UseItem("POTN17",Myself)
END

IF
  HasItem("POTN20",Myself)
  StateCheck(Myself,STATE_POISONED)
  !GlobalTimerNotExpired("POT","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",6)
    UseItem("POTN20",Myself)
END

IF
  ActionListEmpty()
  StateCheck(Myself,STATE_POISONED)
  !GlobalTimerNotExpired("POIS","LOCALS")
  See([EVILCUTOFF])
  !HasItem("POTN17",Myself)
  !HasItem("POTN20",Myself)
THEN
  RESPONSE #100
    Shout(83)
    DisplayStringHead(Myself,@1)
    SetGlobalTimer("POIS","LOCALS",6)
END

IF
  ActionListEmpty()
  HasItem("mage01",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("BPRING05","LOCALS",0)
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_INVISIBLE)
  RandomNumLT(4,4)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    SetGlobal("BPRING05","LOCALS",1)
    DisplayStringHead(Myself,47318)
    UseItem("mage01",Myself)
END

IF
  ActionListEmpty()
  HasItem("ring05",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("BPRING05","LOCALS",0)
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_INVISIBLE)
  RandomNumLT(5,4)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    SetGlobal("BPRING05","LOCALS",1)
    DisplayStringHead(Myself,47318)
    UseItem("ring05",Myself)
END

IF
  ActionListEmpty()
  HasItem("potn10",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_INVISIBLE)
  RandomNumLT(6,5)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,46150)
    UseItem("potn10",Myself)
END

IF
  ActionListEmpty()
  HaveSpellRES("spin118")
  !GlobalTimerNotExpired("BPT","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
  !StateCheck(Myself,STATE_INVISIBLE)
  RandomNumLT(16,11)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,46150)
    SpellRES("spin118",Myself)
END

IF
  ActionListEmpty()
  HasItem("potn14",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_HASTED)
  RandomNumLT(3,3)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    DisplayStringHead(Myself,46150)
    UseItem("potn14",Myself)
END

IF
  ActionListEmpty()
  HasItem("POTN36",Myself)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(7,3)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    UseItem("POTN36",Myself)
    DisplayStringHead(Myself,46150)
END

IF
  ActionListEmpty()
  OR(2)
    Class(Myself,FIGHTER_THIEF)
    Class(Myself,FIGHTER_MAGE_THIEF)
  HitBy([GOODCUTOFF],CRUSHING)
  !GlobalTimerNotExpired("RANG","LOCALS")
  !StateCheck(Myself,STATE_POISONED)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  !Range(LastSeenBy(Myself),7)
  !TargetUnreachable(LastSeenBy(Myself))
  !InWeaponRange(LastSeenBy(Myself))
  HPGT(LastSeenBy(Myself),0)
THEN
  RESPONSE #50
    DisplayStringHead(Myself,@2)
    SetGlobalTimer("RANG","LOCALS",6)
  RESPONSE #50
    DisplayStringHead(Myself,@3)
    SetGlobalTimer("RANG","LOCALS",6)
  RESPONSE #25
    DisplayStringHead(Myself,@4)
    SetGlobalTimer("RANG","LOCALS",6)
  RESPONSE #25
    DisplayStringHead(Myself,@5)
    SetGlobalTimer("RANG","LOCALS",18)
    EquipMostDamagingMelee()
    MoveToObject(LastSeenBy(Myself))
    AttackReevaluate(LastSeenBy(Myself),180)
END



IF
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(28,22)
  OR(4)
    See([GOODCUTOFF.0.0.0.0.SUMMONED_DEMON])
    See([GOODCUTOFF.0.0.0.0.SUMMONED])
    See([GOODCUTOFF.0.0.0.207])
    See([GOODCUTOFF.0.0.0.0.ILLUSIONARY])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(31,26)
  OR(6)
    See(NearestEnemyOf(Myself))
    See(WeakestOf)
    See(WorstAC)
    See(MostDamagedOf)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(24,9)
  OR(3)
    See(StrongestOf)
    See(BestAC)
    See(LeastDamagedOf)
  RandomNumLT(25,9)
  See(WeakestOf)
  RandomNumLT(23,8)
  See(WorstAC)
  RandomNumLT(21,8)
  See(MostDamagedOf)
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(32,25)
  OR(3)
    Heard(ThirdNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(SecondNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(NearestMyGroupOfType([EVILCUTOFF]),124)
  HPGT(LastHeardBy(Myself),1)
  Range(LastHeardBy(Myself),20)
  Exists(LastAttackerOf(LastHeardBy(Myself)))
  !Allegiance(LastAttackerOf(LastHeardBy(Myself)),EVILCUTOFF)
  !StateCheck(LastAttackerOf(LastHeardBy(Myself)),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(LastHeardBy(Myself)),MORNINGSTAR)
    !Class(LastAttackerOf(LastHeardBy(Myself)),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(LastHeardBy(Myself)),65)
  OR(3)  
    !StateCheck(LastAttackerOf(LastHeardBy(Myself)),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(LastHeardBy(Myself)))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(33,13)
  HitBy([0],0x0000)
  !Allegiance(LastHitter(Myself),EVILCUTOFF)
  !StateCheck(LastHitter(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
  OR(3)  
    !StateCheck(LastHitter(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumGT(33,22)
  AttackedBy([0],DEFAULT)
  !Allegiance(LastAttackerOf(Myself),EVILCUTOFF)
  !StateCheck(LastAttackerOf(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(Myself),MORNINGSTAR)
    !Class(LastAttackerOf(Myself),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(Myself),65)
  OR(3)  
    !StateCheck(LastAttackerOf(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    Allegiance(LastSeenBy(Myself),ENEMY)
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODBUTRED)
THEN
  RESPONSE #100
END


IF
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
THEN
  RESPONSE #80
    Continue()
  RESPONSE #20
END



IF
  ActionListEmpty()
  StateCheck(Myself,0x00400010)
  HPPercentGT(Myself,25)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",12)
    EquipMostDamagingMelee()
    MoveToObject(LastSeenBy(Myself))
    FollowObjectFormation(LastSeenBy(Myself),1,3)
    SmallWait(5)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  HaveSpell(ASSASSIN_POISON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(6,5)
  Range(LastSeenBy(Myself),6)
  CheckStat(LastSeenBy(Myself),0,128)
  CheckStat(Myself,0,SCRIPTINGSTATE4)
  !General(LastSeenBy(Myself),UNDEAD)
  !StateCheck(LastSeenBy(Myself),STATE_POISONED)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSDEATH)
  CheckStatLT(LastSeenBy(Myself),51,RESISTPOISON)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",6)
    Spell(Myself,ASSASSIN_POISON)
    DisplayStringHead(Myself,52056)
    AttackOneRound(LastSeenBy(Myself))
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  !GlobalTimerNotExpired("BPW","LOCALS")
  !Global("BP_PlotMan","LOCALS",1)
  Detect(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_INVISIBLE)
  !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
  RESPONSE #70
    SetGlobalTimer("BPW","LOCALS",6)
    DisplayStringHead(Myself,@6)
    Shout(124)
    RunAwayFromNoInterrupt(NearestEnemyOf(Myself),90)
  RESPONSE #10
    SetGlobalTimer("BPW","LOCALS",9)
    DisplayStringHead(Myself,@7)
    Shout(124)
    EquipMostDamagingMelee()
    Attack(LastHitter(Myself))
  RESPONSE #20
    SetGlobalTimer("BPW","LOCALS",9)
    DisplayStringHead(Myself,@8)
    Shout(123)
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  CheckStatGT(LastSeenBy(Myself),10,180)
  CheckStatLT(Myself,11,180)
THEN
  RESPONSE #100
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  OR(3)
    !Range(NearestEnemyOf(Myself),5)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #100
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
END

IF
  ActionListEmpty()
  !Range(LastSeenBy(Myself),8)
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
THEN
  RESPONSE #90
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
  RESPONSE #90
    EquipMostDamagingMelee()
    AttackReevaluate(LastSeenBy(Myself),135)
END

IF
  ActionListEmpty()
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END

