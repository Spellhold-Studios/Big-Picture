IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  OR(2)
    Heard([NEUTRAL],79)
    Heard([EVILCUTOFF],79)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  ActionListEmpty()
  See([PC])
  Allegiance(Myself,EVILCUTOFF)
  !GlobalTimerNotExpired("DUE2","LOCALS")
  GlobalLT("GROW","LOCALS",3)
  Global("GIANT","LOCALS",0)
  HPPercentGT(Myself,50)
  RandomNumLT(101,41)
THEN
  RESPONSE #100
    SetGlobalTimer("DUE2","LOCALS",FOUR_ROUNDS)
    IncrementGlobal("GROW","LOCALS",1)
    DisplayStringHead(Myself,@0)
    ReallyForceSpellRES("BPDUEGIA",Myself)
    SetGlobal("GIANT","LOCALS",1)
    SetGlobal("BOW","LOCALS",2)
    SetGlobal("MM","LOCALS",1)
END

IF
  OR(2)
    HPPercentLT(Myself,25)
    GlobalTimerExpired("DUE2","LOCALS")
  Global("GIANT","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("DUE2","LOCALS",FOUR_ROUNDS)
    DisplayStringHead(Myself,@1)
    ReallyForceSpellRES("BPDUEGA2",Myself)
    SetGlobal("GIANT","LOCALS",0)
    SetGlobal("BOLD","LOCALS",0)
    SetGlobal("BOW","LOCALS",0)
    SetGlobal("MM","LOCALS",0)
END

