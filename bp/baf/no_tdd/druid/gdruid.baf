IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  Range(NearestEnemyOf(Myself),2)
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(NearestEnemyOf(Myself),0,125)
THEN
  RESPONSE #80
    RunAwayFrom(NearestEnemyOf(Myself),4)
  RESPONSE #20
    Continue()
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("FORM","LOCALS")
  Global("SALAM","LOCALS",0)
  Global("MORPH","LOCALS",0)
  OR(3)
    HPPercentLT(Myself,35)
    RandomNum(44,1)
    !HaveAnySpells()
  !HasItem("FBLADE",Myself)  // ~Flame Blade~
  !HasItem("SHILLE",Myself)  // ~Shillelagh~
THEN
  RESPONSE #100
    ForceSpellRES("SPIN127",Myself)  // ~Shapeshifts Fire Salamander~
    SetGlobalTimer("FORM","LOCALS",FIVE_ROUNDS)
    SetGlobal("SALAM","LOCALS",1)
    SetGlobal("MORPH","LOCALS",1)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("FORM","LOCALS")
  Global("WYVERN","LOCALS",0)
  Global("MORPH","LOCALS",0)
  OR(3)
    HPPercentLT(Myself,35)
    RandomNum(45,1)
    !HaveAnySpells()
  !HasItem("FBLADE",Myself)  // ~Flame Blade~
  !HasItem("SHILLE",Myself)  // ~Shillelagh~
THEN
  RESPONSE #100
    ForceSpellRES("SPIN126",Myself)  // ~Shapeshifts Baby Wyvern~
    SetGlobalTimer("FORM","LOCALS",FIVE_ROUNDS)
    SetGlobal("WYVERN","LOCALS",1)
    SetGlobal("MORPH","LOCALS",1)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("FORM","LOCALS")
  Global("SPIDER","LOCALS",0)
  Global("MORPH","LOCALS",0)
  OR(3)
    HPPercentLT(Myself,35)
    RandomNum(46,1)
    !HaveAnySpells()
  !HasItem("FBLADE",Myself)  // ~Flame Blade~
  !HasItem("SHILLE",Myself)  // ~Shillelagh~
THEN
  RESPONSE #100
    ForceSpellRES("SPIN125",Myself)  // ~Shapeshifts Spider~
    SetGlobalTimer("FORM","LOCALS",FIVE_ROUNDS)
    SetGlobal("SPIDER","LOCALS",1)
    SetGlobal("MORPH","LOCALS",1)
END

IF
  Global("SNAFU","LOCALS",0)
  OR(3)
    CheckStatGT(Myself,0,SCRIPTINGSTATE4)
    CheckStatGT(Myself,60,SPELLFAILUREPRIEST)
    StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobal("SNAFU","LOCALS",1)
END

IF
  Global("SNAFU","LOCALS",1)
  CheckStat(Myself,0,SCRIPTINGSTATE4)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobal("SNAFU","LOCALS",0)
END

IF
  HPPercentLT(Myself,30)
  GlobalLT("GreatCheat","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("GreatCheat","LOCALS",1)
    SpellNoDec(Myself,CLERIC_REGENERATE)
    ForceSpell(Myself,CLERIC_HEAL)
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("POT","LOCALS")
  HasItem("POTN52",Myself)  // ~Potion of Extra Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN52",Myself)  // ~Potion of Extra Healing~
END

IF
  Global("MORPH","LOCALS",1)
  Global("SALAM","LOCALS",1)
  OR(3)
    HPPercentLT(Myself,20)
    RandomNumLT(48,3)
    GlobalTimerExpired("FORM","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("SPIN122",Myself)  // ~Shapeshifts Natural Form~
    RemoveSpell(SALAMANDER_BREATHE_FIREBALL)
    RemoveSpell(SALAMANDER_BREATHE_FIREBALL)
    RemoveSpell(SALAMANDER_BREATHE_FIREBALL)
    SetGlobal("FORM","LOCALS",0)
    SetGlobal("MORPH","LOCALS",0)
    SetGlobal("SALAM","LOCALS",2)
END

IF
  Global("MORPH","LOCALS",1)
  Global("WYVERN","LOCALS",1)
  OR(3)
    HPPercentLT(Myself,20)
    RandomNumLT(49,3)
    GlobalTimerExpired("FORM","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("SPIN122",Myself)  // ~Shapeshifts Natural Form~
    SetGlobal("FORM","LOCALS",0)
    SetGlobal("MORPH","LOCALS",0)
    SetGlobal("WYVERN","LOCALS",2)
END

IF
  Global("MORPH","LOCALS",1)
  Global("SPIDER","LOCALS",1)
  OR(3)
    HPPercentLT(Myself,20)
    RandomNumLT(50,3)
    GlobalTimerExpired("FORM","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("SPIN122",Myself)  // ~Shapeshifts Natural Form~
    SetGlobal("FORM","LOCALS",0)
    SetGlobal("MORPH","LOCALS",0)
    SetGlobal("SPIDER","LOCALS",2)
END

IF
  OR(4)
    Global("MORPH","LOCALS",1)
    Global("SNAFU","LOCALS",1)
    GlobalTimerNotExpired("BPT","LOCALS")
    !HaveAnySpells()
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),45)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,40)
  HaveSpell(CLERIC_HEAL)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("SNAFU","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_HEAL)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_REGENERATE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("SNAFU","LOCALS",0)
  CheckStat(Myself,0,124)
  HPPercentLT(Myself,50)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_REGENERATE)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobalTimer("CHE","LOCALS",THREE_ROUNDS)
    ReallyForceSpell(Myself,CLERIC_IRONSKIN)
    ReallyForceSpell(Myself,CLERIC_SHIELD_OF_THE_ARCHONS)
    ForceSpell(Myself,CLERIC_REGENERATE)
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  Global("Prep","LOCALS",1)
  GlobalTimerExpired("CHE","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",2)
    SetGlobalTimer("CHE","LOCALS",THREE_ROUNDS)
    ReallyForceSpell(Myself,CLERIC_PROTECTION_FROM_LIGHTNING)
    ReallyForceSpell(Myself,CLERIC_PROTECTION_FROM_FIRE)
    ForceSpell(Myself,CLERIC_MAGIC_RESISTANCE)
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  Global("Prep","LOCALS",2)
  GlobalTimerExpired("CHE","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",3)
    SetGlobalTimer("CHE","LOCALS",SEVEN_ROUNDS)
    ReallyForceSpell(Myself,CLERIC_FREE_ACTION)
    ReallyForceSpell(Myself,CLERIC_CHAOTIC_COMMANDS)
    ForceSpell(Myself,CLERIC_DEATH_WARD)
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  Global("Prep","LOCALS",3)
  GlobalTimerExpired("CHE","LOCALS")
  CheckStat(Myself,0,124)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",4)
    ForceSpell(Myself,CLERIC_REGENERATE)
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,65)
  HaveSpell(CLERIC_CURE_SERIOUS_WOUNDS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("SNAFU","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,70)
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("SNAFU","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  Global("BP_SweetZone","LOCALS",0)
  OR(4)
    CheckStat(Myself,11,180)
    CheckStat(Myself,16,180)
    CheckStat(Myself,17,180)
    CheckStat(Myself,20,180)
THEN
  RESPONSE #50
    SetGlobal("BP_SweetZone","LOCALS",1)
    DisplayStringHead(Myself,@0)
    ForceSpell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
  RESPONSE #50
    SetGlobal("BP_SweetZone","LOCALS",1)
END
//////////////CLOUDLIKE  EFFECTS//////////////////////

IF
  CheckStat(Myself,11,180) // I'm in a stinking cloud
  CheckStatGT(Myself,5,INT)
  !StateCheck(Myself,STATE_SLEEPING) // not yet affected
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(2)
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStatGT(Myself,11,180) // I'm in a web,entangle, or grease spell
  CheckStatLT(Myself,15,180)
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,1,131) // I don't have free action
  CheckStatLT(Myself,1,WEB) //and not webbed up
  CheckStatLT(Myself,1,ENTANGLE) //nor entangled --I can move
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,15,180) // I'm in an Ice Storm
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTCOLD) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,16,180) // I'm in a Cloudkill or Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTPOISON) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,17,180) // I'm in a Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTACID) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,18,180) // Incendiary Cloud, Meteor Swarm or Fire Storm -- bad stuff
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTFIRE) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

// This is to try and keep them from going back in to whomp somebody
IF
  CheckStatLT(Myself,11,180) // No longer in the cloud effects
  CheckStatGT(Myself,5,INT)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  !Range(LastSeenBy(Myself),16)
  Exists([EVILCUTOFF])
  !Range(NearestEnemyOf(Myself),2)
THEN
  RESPONSE #100
    EquipRanged()  //use your bow, then
    SmallWait(3)
    Continue()
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

IF
  ActionListEmpty()
  See([PC.0.0.DRUID_ALL])
  OR(2)
    HasItem("SHILLE",Myself)  // ~Shillelagh~
    HasItem("FBLADE",Myself)  // ~Flame Blade~
  HPPercentGT(Myself,35)
THEN
  RESPONSE #75
    AttackReevaluate(LastSeenBy(Myself),45)
  RESPONSE #25
    Continue()
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_SANCTUARY)
  !GlobalTimerNotExpired("BPT","LOCALS")
  !Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNum(4,1)
  HPPercentLT(Myself,50)
  CheckStat(Myself,0,SANCTUARY)
THEN
  RESPONSE #100
    SetGlobalTimer("BPIP","LOCALS",15)
    Spell(Myself,CLERIC_SANCTUARY)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_IRONSKIN)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  CheckStat(Myself,0,STONESKINS)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(Myself,CLERIC_IRONSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_NEUTRALIZE_POISON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  HPPercentLT(Myself,51)
  StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(Myself,CLERIC_NEUTRALIZE_POISON)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_SLOW_POISON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  HPPercentLT(Myself,51)
  StateCheck(Myself,STATE_POISONED)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(Myself,CLERIC_SLOW_POISON)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_TRUE_SIGHT)
  CheckStat(Myself,0,118)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  CheckStat(Myself,0,118)
  Exists(NearestEnemyOf(Myself))
  OR(9)
    SpellCast([GOODCUTOFF],WIZARD_PROJECT_IMAGE)
    SpellCast([GOODCUTOFF],WIZARD_MISLEAD)
    StateCheck(NearestEnemyOfType([GOODCUTOFF]),0x60400010)
    StateCheck(SecondNearestEnemyOfType([GOODCUTOFF]),0x60400010)
    StateCheck(ThirdNearestEnemyOfType([GOODCUTOFF]),0x60400010)
    StateCheck(FourthNearestEnemyOfType([GOODCUTOFF]),0x60400010)
    StateCheck(FifthNearestEnemyOfType([GOODCUTOFF]),0x60400010)
    StateCheck(SixthNearestEnemyOfType([GOODCUTOFF]),0x60400010)
    NumCreatureGT([GOODCUTOFF.0.0.0.0.ILLUSIONARY],0)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,99401)  // ~Hmmm....Something's not right here...~
    Spell(Myself,CLERIC_TRUE_SIGHT)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_INVISIBILITY_PURGE)
  CheckStat(Myself,0,118)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  CheckStatLT(Myself,1,118)
  CheckStatLT(Myself,1,SEEINVISIBLE)
  !See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_BLIND)
  OR(4)
    SpellCast([GOODCUTOFF],WIZARD_IMPROVED_INVISIBILITY)
    SpellCast([GOODCUTOFF],WIZARD_INVISIBILITY)
    Detect(NearestEnemyOf(Myself))
    StateCheck(LastSeenBy(Myself),0x400010)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@6)
    Spell(Myself,CLERIC_INVISIBILITY_PURGE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_RESIST_FIRE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(10,7)
  OR(3)
    HitBy([ANYONE],FIRE)
    HitBy([ANYONE],COLD)
    RandomNumLT(12,8)
  OR(3)
    CheckStatLT(Myself,41,RESISTFIRE)
    CheckStatLT(Myself,41,RESISTCOLD)
  !StateCheck(Myself,STATE_SILENCED)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_RESIST_FIRE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,-4,ARMORCLASS)
  CheckStatGT(Myself,19,LEVEL)
  CheckStatGT(Myself,20,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,-3,ARMORCLASS)
  CheckStatGT(Myself,15,LEVEL)
  OR(2)
    CheckStatGT(Myself,19,LEVEL)
    CheckStatGT(Myself,20,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,-2,ARMORCLASS)
  CheckStatGT(Myself,11,LEVEL)
  OR(2)
    CheckStatGT(Myself,19,LEVEL)
    CheckStatGT(Myself,17,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,-1,ARMORCLASS)
  CheckStatGT(Myself,7,LEVEL)
  OR(2)
    CheckStatGT(Myself,19,LEVEL)
    CheckStatGT(Myself,16,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,0,ARMORCLASS)
  CheckStatGT(Myself,3,LEVEL)
  OR(2)
    CheckStatGT(Myself,19,LEVEL)
    CheckStatGT(Myself,15,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,1,ARMORCLASS)
  OR(2)
    CheckStatGT(Myself,19,LEVEL)
    CheckStatGT(Myself,14,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,2,ARMORCLASS)
  CheckStatLT(Myself,20,LEVEL)
  OR(2)
    CheckStatGT(Myself,15,LEVEL)
    CheckStatGT(Myself,14,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,3,ARMORCLASS)
  CheckStatLT(Myself,16,LEVEL)
  OR(2)
    CheckStatGT(Myself,11,LEVEL)
    CheckStatGT(Myself,14,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,4,ARMORCLASS)
  CheckStatLT(Myself,12,LEVEL)
  OR(2)
    CheckStatGT(Myself,7,LEVEL)
    CheckStatGT(Myself,14,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatGT(Myself,5,ARMORCLASS)
  CheckStatLT(Myself,8,LEVEL)
  OR(2)
    CheckStatGT(Myself,3,LEVEL)
    CheckStatGT(Myself,14,DEX)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_BARKSKIN)
  CheckStatLT(Myself,4,LEVEL)
  CheckStatGT(Myself,6,ARMORCLASS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(13,5)
  CheckStat(Myself,0,SCRIPTINGSTATE5)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_BARKSKIN)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(9,4)
  CheckStatLT(Myself,4,SCRIPTINGSTATE6)
  NumCreatureGT([GOODCUTOFF],2)
  Range(ThirdNearestEnemyOf(Myself),10)
  OR(3) // Be sure at least somebody offers a melee-worthy challenge
    HPGT(NearestEnemyOfType([GOODCUTOFF]),40)
    HPGT(SecondNearestEnemyOfType([GOODCUTOFF]),40)
    HPGT(ThirdNearestEnemyOfType([GOODCUTOFF]),40)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_ARMOR_OF_FAITH)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  RandomNumLT(14,5)
  CheckStatLT(Myself,10,SCRIPTINGSTATE5)
  CheckStatLT(Myself,1,110)
  NumCreatureGT([GOODCUTOFF],2)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_ARMOR_OF_FAITH)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_DISPEL_MAGIC)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  !StateCheck(Myself,0x2208010)
  !StateCheck(Myself,0x62000000)
  OR(3)
    StateCheck(Myself,270381)
    StateCheck(Myself,0x80100000)
    StateCheck(Myself,STATE_SLOWED)
  OR(4) // Either nobody's close, or we're protected in some way from melee
    !Range(NearestEnemyOf(Myself),3)
    CheckStatGT(Myself,0,STONESKINS)
    CheckStatGT(Myself,0,181)
    CheckStatGT(Myself,0,128)      
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_DISPEL_MAGIC)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_DISPEL_MAGIC)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  Exists(SecondNearestMyGroupOfType)
  Range(SecondNearestMyGroupOfType,15)
  !StateCheck(SecondNearestMyGroupOfType,0x400010)
  !StateCheck(SecondNearestMyGroupOfType,0x2208010)
  !StateCheck(SecondNearestMyGroupOfType,0x62000000)
  OR(8)
    CheckStatGT(SecondNearestMyGroupOfType,0,HELD)
    CheckStatGT(SecondNearestMyGroupOfType,0,ENTANGLE)
    CheckStatGT(SecondNearestMyGroupOfType,0,GREASE)
    CheckStatGT(SecondNearestMyGroupOfType,0,WEB)
    StateCheck(SecondNearestMyGroupOfType,270381)
    StateCheck(SecondNearestMyGroupOfType,0x80100000)
    StateCheck(SecondNearestMyGroupOfType,STATE_SILENCED)
    StateCheck(SecondNearestMyGroupOfType,STATE_SLOWED)
  OR(4) // Either nobody's close, or we're protected in some way from melee
    !Range(NearestEnemyOf(Myself),3)
    CheckStatGT(Myself,0,STONESKINS)
    CheckStatGT(Myself,0,181)
    CheckStatGT(Myself,0,128)      
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(SecondNearestMyGroupOfType,CLERIC_DISPEL_MAGIC)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_REMOVE_FEAR)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  Global("SNAFU","LOCALS",0)
  !StateCheck(ThirdNearestMyGroupOfType([EVILCUTOFF]),0x400010)
  OR(6)
    StateCheck(SixthNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(FifthNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(FourthNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(ThirdNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(SecondNearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
    StateCheck(NearestMyGroupOfType([EVILCUTOFF]),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(ThirdNearestMyGroupOfType([EVILCUTOFF]),CLERIC_REMOVE_FEAR)
END

IF
  ActionListEmpty()
  OR(2)
    StateCheck(Myself,STATE_INVISIBLE)
    CheckStatGT(Myself,0,SANCTUARY)
  GlobalTimerNotExpired("BPIP","LOCALS")
THEN
  RESPONSE #15
    Continue()
  RESPONSE #60
    SetGlobalTimer("BPT","LOCALS",0)
  RESPONSE #90
    SetGlobalTimer("BPT","LOCALS",0)
    RunAwayFrom(NearestEnemyOf(Myself),10)
END

IF
  OR(4)
    See(NearestEnemyOf(Myself))
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
    See([PC.0.0.DRUID_ALL])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(26,18)
  OR(9)
    See([GOODCUTOFF.0.0.MONK])
    See([GOODCUTOFF.0.0.BARD_ALL])
    See([GOODCUTOFF.0.0.THIEF_ALL])
    See([GOODCUTOFF.0.0.RANGER_ALL])
    See([GOODCUTOFF.0.0.PALADIN_ALL])
    See([GOODCUTOFF.0.0.LONG_BOW])
    See([GOODCUTOFF.0.0.CLERIC_ALL])
    See([GOODCUTOFF.0.0.SORCERER])
    See([GOODCUTOFF.0.0.DRUID_ALL])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(29,23)
  OR(7)
    See(SecondNearestEnemyOfType([PC.0.0.RANGER_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.LONG_BOW]))
    See(SecondNearestEnemyOfType([PC.0.0.SORCERER]))
    See(SecondNearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.DRUID_ALL]))
  RandomNumLT(27,23)
  OR(7)
    See(ThirdNearestEnemyOfType([PC.0.0.RANGER_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.LONG_BOW]))
    See(ThirdNearestEnemyOfType([PC.0.0.SORCERER]))
    See(ThirdNearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.DRUID_ALL]))
  RandomNumLT(32,18)
  OR(4)
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.LONG_BOW]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
  RandomNumLT(20,8)
  See(TenthNearestEnemyOf(Myself))
  RandomNumLT(30,6)
  See(Protagonist)
  False()
THEN
  RESPONSE #100
    Continue()
END


IF
  ActionListEmpty()
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(35,25)
  !StateCheck(LastSeenBy(Myself),STATE_SILENCED)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23) //or we'll just shoot the moon
  CheckStatGT(LastSeenBy(Myself),3,SAVEVSSPELL) // at -5 gives a 40% chance
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
  OR(3)
    !Range(LastSeenBy(Myself),16)
    !GlobalTimerNotExpired("BPVOCAL","LOCALS")
    CheckStatGT(Myself,74,RESISTMAGIC)
  OR(6)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,2)
    CheckStatLT(NearestMyGroupOfType,6,SAVEVSSPELL) // save at -5, thus 50% change
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
    CheckStatGT(NearestMyGroupOfType,0,MINORGLOBE)
    StateCheck(NearestMyGroupOfType,STATE_SILENCED)
  OR(6)
    !Exists(SecondNearestMyGroupOfType)
    Range(SecondNearestMyGroupOfType,3)
    CheckStatLT(NearestMyGroupOfType,6,SAVEVSSPELL)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
    CheckStatGT(NearestMyGroupOfType,0,MINORGLOBE)
    StateCheck(NearestMyGroupOfType,STATE_SILENCED)
  OR(6)
    !Exists(ThirdNearestMyGroupOfType)
    Range(ThirdNearestMyGroupOfType,4)
    CheckStatLT(ThirdNearestMyGroupOfType,6,SAVEVSSPELL)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTMAGIC)
    CheckStatGT(ThirdNearestMyGroupOfType,0,MINORGLOBE)
    StateCheck(ThirdNearestMyGroupOfType,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_INSECT_PLAGUE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(37,26)
  CheckStat(LastSeenBy(Myself),0,120)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129)
    RandomNumLT(99,23)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_ANIMATE_TREE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(30,23)
  !AreaType(DUNGEON)
  !AreaType(CITY)
  NumCreatureLT([EVILCUTOFF.0.0.0.0.SUMMONED],7)
  NumCreatureLT([EVILCUTOFF.0.TREANT.0.0.SUMMONED],3)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_ANIMATE_TREE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CONJURE_EARTH_ELEMENTAL)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(31,19)
  NumCreatureLT([EVILCUTOFF.0.0.0.0.SUMMONED],7)
  NumCreatureLT([EVILCUTOFF.0.ELEMENTAL.0.0.SUMMONED],3)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(5) // Either nobody's close, or we're protected in some way from melee &/or missile
    !Range(NearestEnemyOf(Myself),3) // not the 3-round blitz of the mage spell, proceed as normal
    CheckStatGT(Myself,0,179)
    CheckStatGT(Myself,0,STONESKINS)
    CheckStatGT(Myself,0,181)
    CheckStatGT(Myself,0,128)      
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CONJURE_EARTH_ELEMENTAL)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CONJURE_ANIMALS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(32,19)
  NumCreatureLT([EVILCUTOFF.0.0.0.0.SUMMONED],7)
  NumCreatureLT([EVILCUTOFF.ANIMAL.0.0.0.SUMMONED],5)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(5) // Either nobody's close, or we're protected in some way from melee &/or missile
    !Range(NearestEnemyOf(Myself),3) // not the 3-round blitz of the mage spell, proceed as normal
    CheckStatGT(Myself,0,179)
    CheckStatGT(Myself,0,STONESKINS)
    CheckStatGT(Myself,0,181)
    CheckStatGT(Myself,0,128)      
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CONJURE_ANIMALS)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_DOLOROUS_DECAY)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(49,30)
  CheckStat(LastSeenBy(Myself),0,120)
  !StateCheck(LastSeenBy(Myself),0x14000) //slowed or poisoned
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23) //or we'll just shoot the moon
  CheckStatGT(LastSeenBy(Myself),7,SAVEVSDEATH)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_DOLOROUS_DECAY)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_FLAME_STRIKE)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(45,28)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23)
  !StateCheck(LastSeenBy(Myself),0x00400010)
  CheckStatLT(LastSeenBy(Myself),51,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_FLAME_STRIKE)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CALL_WOODLAND_BEINGS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(34,18)
  NumCreatureLT([EVILCUTOFF.0.0.0.0.SUMMONED],7)
  NumCreatureLT([EVILCUTOFF.0.FAIRY.FAIRY_NYMPH.0.SUMMONED],3)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(5) // Either nobody's close, or we're protected in some way from melee &/or missile
    !Range(NearestEnemyOf(Myself),3) // not the 3-round blitz of the mage spell, proceed as normal
    CheckStatGT(Myself,0,179)
    CheckStatGT(Myself,0,STONESKINS)
    CheckStatGT(Myself,0,181)
    CheckStatGT(Myself,0,128)      
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CALL_WOODLAND_BEINGS)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CALL_LIGHTNING)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(50,34)
  AreaType(OUTDOOR)
  CheckStatLT(LastSeenBy(Myself),51,RESISTELECTRICITY)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23)
  !StateCheck(LastSeenBy(Myself),0x00400010)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CALL_LIGHTNING)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_POISON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(52,31)
  !StateCheck(LastSeenBy(Myself),STATE_POISONED)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(6)
    HPGT(LastSeenBy(Myself),29)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_POISON)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_CLOAK_OF_FEAR)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(57,27)
  !General(LastSeenBy(Myself),UNDEAD)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  CheckStatLT(LastSeenBy(Myself),1,178)
  !StateCheck(LastSeenBy(Myself),0x100029) // feeblemind,helpless,stunned,sleeping
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
  CheckStat(LastSeenBy(Myself),0,HELD)
  CheckStat(LastSeenBy(Myself),0,WEB)
  OR(3)
    Range(LastSeenBy(Myself),10)
    Range(SecondNearestEnemyOf(Myself),12)
    Range(ThirdNearestEnemyOf(Myself),15)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_CLOAK_OF_FEAR)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_ANIMAL_SUMMONING_1)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(36,18)
  NumCreatureLT([EVILCUTOFF.0.0.0.0.SUMMONED],7)
  NumCreatureLT([EVILCUTOFF.ANIMAL.0.0.0.SUMMONED],5)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_ANIMAL_SUMMONING_1)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_SUMMON_INSECTS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(39,22)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129)
    RandomNumLT(99,23)
  CheckStat(LastSeenBy(Myself),0,120)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_SUMMON_INSECTS)
END

IF
  ActionListEmpty()
  HaveSpell(SALAMANDER_BREATHE_FIREBALL)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",1)
  Global("SALAM","LOCALS",1)
  RandomNumLT(55,25)
  !StateCheck(LastSeenBy(Myself),0x00400010)
  CheckStatLT(LastSeenBy(Myself),76,RESISTFIRE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(Myself,61,194)
  OR(2)
    !Range(LastSeenBy(Myself),15)
    CheckStatGT(Myself,74,RESISTFIRE)
  OR(4)
    !Exists(NearestMyGroupOfType)
    Range(NearestMyGroupOfType,3)
    CheckStatGT(NearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(NearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(SecondNearestMyGroupOfType)
    Range(SecondNearestMyGroupOfType,4)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(SecondNearestMyGroupOfType,49,RESISTMAGIC)
  OR(4)
    !Exists(ThirdNearestMyGroupOfType)
    Range(ThirdNearestMyGroupOfType,5)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTFIRE)
    CheckStatGT(ThirdNearestMyGroupOfType,49,RESISTMAGIC)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),SALAMANDER_BREATHE_FIREBALL)
END

IF
  ActionListEmpty()
  General(LastSeenBy(Myself),ANIMAL)
  HaveSpell(CLERIC_HOLD_ANIMAL)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(40,19)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129)
    RandomNumLT(99,23)
  CheckStatLT(LastSeenBy(Myself),1,131) // Free Action
  !StateCheck(LastSeenBy(Myself),0x00400010) // STATE_NOT_VISIBLE
  !StateCheck(LastSeenBy(Myself),0x102029) // STATE_HARMLESS
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
  CheckStatLT(LastSeenBy(Myself),1,HELD)
  CheckStatLT(LastSeenBy(Myself),1,WEB)
  CheckStatLT(LastSeenBy(Myself),1,ENTANGLE)
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_HOLD_ANIMAL)
END

IF
  ActionListEmpty()
  OR(2)
    General(LastSeenBy(Myself),HUMANOID)
    General(LastSeenBy(Myself),ANIMAL)
  HaveSpell(CLERIC_CHARM_PERSON)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(44,21)
  !General(LastSeenBy(Myself),UNDEAD)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23) //or we'll just shoot the moon
  !StateCheck(LastSeenBy(Myself),0x102029) // state_harmless
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
  !StateCheck(LastSeenBy(Myself),0x00400010) // STATE_NOT_VISIBLE
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(6)
    HPGT(LastSeenBy(Myself),25)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_CHARM_PERSON)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_DOOM)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(55,24)
  OR(2)
    CheckStat(LastSeenBy(Myself),0,129) // either they have no spell turning
    RandomNumLT(99,23) //or we'll just shoot the moon
  CheckStatGT(LastSeenBy(Myself),5,SAVEVSSPELL)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
  OR(6)
    Class(LastSeenBy(Myself),MONK)
    Class(LastSeenBy(Myself),FIGHTER_ALL)
    Class(LastSeenBy(Myself),RANGER_ALL)
    Class(LastSeenBy(Myself),PALADIN_ALL)
    HPGT(LastSeenBy(Myself),24)
    CheckStatLT(LastSeenBy(Myself),15,THAC0)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_DOOM)
END


IF
  RandomNumLT(23,14)
  OR(3)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.BARD_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.LONG_BOW]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
  RandomNumLT(24,11)
  OR(4)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.RANGER_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
  RandomNumLT(25,10)
  OR(3)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.MONK]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.THIEF_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.BARD_ALL]))
  RandomNumLT(21,7)
  OR(3)
    See(NearestEnemyOfType([GOODCUTOFF.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.RANGER_ALL]))
    See(NearestEnemyOfType([GOODCUTOFF.0.0.PALADIN_ALL]))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(32,25)
  OR(3)
    Heard(ThirdNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(SecondNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(NearestMyGroupOfType([EVILCUTOFF]),124)
  HPGT(LastHeardBy(Myself),1)
  Range(LastHeardBy(Myself),20)
  Exists(LastAttackerOf(LastHeardBy(Myself)))
  !Allegiance(LastAttackerOf(LastHeardBy(Myself)),EVILCUTOFF)
  !StateCheck(LastAttackerOf(LastHeardBy(Myself)),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(LastHeardBy(Myself)),MORNINGSTAR)
    !Class(LastAttackerOf(LastHeardBy(Myself)),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(LastHeardBy(Myself)),65)
  OR(3)  
    !StateCheck(LastAttackerOf(LastHeardBy(Myself)),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(LastHeardBy(Myself)))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(33,13)
  HitBy([0],0x0000)
  !Allegiance(LastHitter(Myself),EVILCUTOFF)
  !StateCheck(LastHitter(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
  OR(3)  
    !StateCheck(LastHitter(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumGT(33,22)
  AttackedBy([0],DEFAULT)
  !Allegiance(LastAttackerOf(Myself),EVILCUTOFF)
  !StateCheck(LastAttackerOf(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(Myself),MORNINGSTAR)
    !Class(LastAttackerOf(Myself),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(Myself),65)
  OR(3)  
    !StateCheck(LastAttackerOf(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    Allegiance(LastSeenBy(Myself),ENEMY)
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODBUTRED)
THEN
  RESPONSE #100
END


IF
  Race(LastSeenBy(Myself),MORNINGSTAR)
  Class(LastSeenBy(Myself),FAIRY_NYMPH)
  Specifics(LastSeenBy(Myself),65)
THEN
  RESPONSE #100
END


IF
  Race(LastSeenBy(Myself),MORNINGSTAR)
  Class(LastSeenBy(Myself),FAIRY_NYMPH)
  Specifics(LastSeenBy(Myself),65)
THEN
  RESPONSE #100
END


IF
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
THEN
  RESPONSE #80
    Continue()
  RESPONSE #20
END


IF
  ActionListEmpty()
  HaveSpell(CLERIC_FLAME_BLADE)
  Range(LastSeenBy(Myself),10)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(56,25)
  Range(LastSeenBy(Myself),4)
  !HasItem("FBLADE",Myself)  // ~Flame Blade~
  !HasItem("SHILLE",Myself)  // ~Shillelagh~
  CheckStat(LastSeenBy(Myself),0,181)
  CheckStatLT(LastSeenBy(Myself),61,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),61,RESISTMAGIC)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_FLAME_BLADE)
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  ActionListEmpty()
  HaveSpell(CLERIC_SHILLELAGH)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Global("MORPH","LOCALS",0)
  RandomNumLT(43,17)
  !HasItem("FBLADE",Myself)  // ~Flame Blade~
  !HasItem("SHILLE",Myself)  // ~Shillelagh~
  Range(LastSeenBy(Myself),4)
  CheckStat(LastSeenBy(Myself),0,181)
  NumCreatureGT([GOODCUTOFF],2)
  CheckStatLT(Myself,61,SPELLFAILUREPRIEST)
THEN
  RESPONSE #100
    SetGlobal("BPT","LOCALS",6)
    Spell(Myself,CLERIC_SHILLELAGH)
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
END

IF
  ActionListEmpty()
  Global("MORPH","LOCALS",0)
  !Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #90
    AttackOneRound(NearestEnemyOf(Myself))
  RESPONSE #90
    EquipMostDamagingMelee()
    MoveToObjectNoInterrupt(LastSeenBy(Myself))
    AttackReevaluate(LastSeenBy(Myself),135)
END

IF
  ActionListEmpty()
  Global("MORPH","LOCALS",0)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Global("MORPH","LOCALS",1)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

