IF
  Die()
THEN
  RESPONSE #100
    Shout(66)
END

IF
  Global("BPCHARM","LOCALS",1)
  !StateCheck(Myself,STATE_CHARMED)
THEN
  RESPONSE #100
    SetGlobal("BPCHARM","LOCALS",0)
    ClearActions(Myself)
    Continue()
END

IF
  GlobalTimerNotExpired("BPCHASE","LOCALS")
  Global("BPCHASER","LOCALS",1)
  OR(2)
    See([GOODCUTOFF])
    See(LastHitter(Myself))
THEN
  RESPONSE #50
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    AttackReevaluate(LastSeenBy(Myself),15)
  RESPONSE #30
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    SetGlobalTimer("RANG","LOCALS",9)
    SetGlobalTimer("DemTel","LOCALS",9)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #20
    DisplayStringHead(Myself,@0)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(Myself,TELEKINESIS)
    SetGlobal("BP_BehTel","LOCALS",1)
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  GlobalTimerExpired("BPCHASE","LOCALS")
  Global("BPCHASER","LOCALS",1)
  !See([GOODCUTOFF])
  !See(LastHitter(Myself))
THEN
  RESPONSE #70
    DisplayStringHead(Myself,@1)
    SetGlobal("BPCHASER","LOCALS",0)
    ClearActions(Myself)
    Continue()
  RESPONSE #30
    DisplayStringHead(Myself,@2)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(LastHitter(Myself),WIZARD_DIMENSION_DOOR)
END

IF
  OR(3)
    AttackedBy([GOODCUTOFF],DEFAULT)
    Heard([NEUTRAL],79)
    Heard([EVILCUTOFF],79)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
    Shout(151)
END

IF
  !Allegiance(Myself,ENEMY)
  HitBy([GOODCUTOFF],CRUSHING)
THEN
  RESPONSE #100
    Shout(151)
    Enemy()
END

IF
  OR(4)
    CheckStat(Myself,11,180)
    CheckStat(Myself,16,180)
    CheckStat(Myself,17,180)
    CheckStat(Myself,20,180)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@3)
    SpellNoDec(Myself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF
  TookDamage()
  Global("BPCHASER","LOCALS",0)
  !See(LastHitter(Myself))
  !See([GOODCUTOFF])
THEN
  RESPONSE #50
    DisplayStringHead(Myself,@4)
    SetGlobalTimer("BPCHASE","LOCALS",12)
    SetGlobal("BPCHASER","LOCALS",1)
    MoveToObject(LastHitter(Myself))
  RESPONSE #25
    DisplayStringHead(Myself,@4)
    RunAwayFrom(LastHitter(Myself),45)
  RESPONSE #25
    DisplayStringHead(Myself,@2)
    SetGlobal("BPCHASER","LOCALS",0)
    ReallyForceSpell(LastHitter(Myself),WIZARD_DIMENSION_DOOR)
END

IF
  CheckStatGT(Myself,10,180)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  CheckStatLT(LastSeenBy(Myself),11,180)
  CheckStatLT(TenthNearest,11,180)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #35
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #65
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@6)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    Wait(1)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_STUN)
    Wait(1)
    ReallyForceSpell(TenthNearest,WIZARD_DIMENSION_DOOR)
END

IF
  CheckStatGT(Myself,10,180)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  CheckStatLT(LastSeenBy(Myself),11,180)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  CheckStatGT(Myself,10,180)
  CheckStatGT(Myself,5,INT)
  OR(2)
    See(TenthNearest([GOODCUTOFF]))
    See(TenthNearest([PC]))
  CheckStatLT(LastSeenBy(Myself),11,180)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",TWO_ROUNDS)
    DisplayStringHead(Myself,@5)
    EquipMostDamagingMelee()
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  CheckStatGT(Myself,10,180)
  CheckStatGT(Myself,5,INT)
  See(TenthNearest)
  CheckStatLT(TenthNearest,11,180)
  !GlobalTimerNotExpired("BPCHASE","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("BPCHASE","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,@5)
    ReallyForceSpell(TenthNearest,WIZARD_DIMENSION_DOOR)
END

//////////////CLOUDLIKE  EFFECTS//////////////////////

IF
  CheckStat(Myself,11,180) // I'm in a stinking cloud
  CheckStatGT(Myself,5,INT)
  !StateCheck(Myself,STATE_SLEEPING) // not yet affected
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(2)
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStatGT(Myself,11,180) // I'm in a web,entangle, or grease spell
  CheckStatLT(Myself,15,180)
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,1,131) // I don't have free action
  CheckStatLT(Myself,1,WEB) //and not webbed up
  CheckStatLT(Myself,1,ENTANGLE) //nor entangled --I can move
  CheckStatGT(Myself,4,SAVEVSSPELL) //and I got a reasonable chance of failing a save throw (25%+)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,15,180) // I'm in an Ice Storm
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTCOLD) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,16,180) // I'm in a Cloudkill or Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTPOISON) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,17,180) // I'm in a Death Fog
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTACID) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

IF
  CheckStat(Myself,18,180) // Incendiary Cloud, Meteor Swarm or Fire Storm -- bad stuff
  CheckStatGT(Myself,5,INT)
  CheckStatLT(Myself,100,RESISTFIRE) // Not Immune
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  Range(LastSeenBy(Myself),16)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    RunAwayFrom(LastSeenBy(Myself),30)
    SetInterrupt(TRUE)
END

// This is to try and keep them from going back in to whomp somebody
IF
  CheckStatLT(Myself,11,180) // No longer in the cloud effects
  CheckStatGT(Myself,5,INT)
  OR(3)
    Detect([0.0.MORNINGSTAR.FAIRY_NYMPH.65])
    Detect(NearestEnemyOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
    Detect(NearestMyGroupOfType([0.0.MORNINGSTAR.FAIRY_NYMPH.65]))
  !Range(LastSeenBy(Myself),16)
  Exists([EVILCUTOFF])
  !Range(NearestEnemyOf(Myself),2)
THEN
  RESPONSE #100
    EquipRanged()  //use your bow, then
    SmallWait(3)
    Continue()
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

IF
  OR(2)
    !Allegiance(Myself,EVILCUTOFF)
    !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
END

IF
  ActionListEmpty()
  SpellCast(Myself,0)
  HaveAnySpells()
  HitBy([ANYONE],CRUSHING)
  See(SecondNearest([EVILCUTOFF]))
  !GlobalTimerNotExpired("SBN","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("SBN","LOCALS",15)
    DisplayStringHead(Myself,@7)
    Shout(69)
END

IF
  Global("BP_BehTel","LOCALS",0)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    ForceSpell(Myself,TELEKINESIS)
    SetGlobal("BP_BehTel","LOCALS",1)
END

IF
  Range(NearestEnemyOf(Myself),2)
  OR(2)
    CheckStatGT(NearestEnemyOf(Myself),0,121)
    CheckStatGT(NearestEnemyOf(Myself),0,125)
THEN
  RESPONSE #80
    RunAwayFrom(NearestEnemyOf(Myself),4)
    ReallyForceSpell(NearestEnemyOf(Myself),DEMON_REPULSION)
  RESPONSE #20
    Continue()
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("POT","LOCALS")
  HasItem("POTN55",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)
    UseItem("POTN55",Myself)
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("POT","LOCALS")
  HasItem("POTN52",Myself)
THEN
  RESPONSE #100
    SetGlobalTimer("POT","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,46150)
    UseItem("POTN52",Myself)
END

IF
  ActionListEmpty()
  HPPercentLT(Myself,35)
  !GlobalTimerNotExpired("BPW","LOCALS")
  !HasItem("POTN52",Myself)
  !HasItem("POTN55",Myself)
  See([EVILCUTOFF])
THEN
  RESPONSE #80
    SetGlobalTimer("BPW","LOCALS",ONE_ROUND)
    DisplayStringHead(Myself,72808)
    Shout(123)
  RESPONSE #20
    Continue()
END

IF
  TargetUnreachable(LastSeenBy(Myself))
  Delay(3)
THEN
  RESPONSE #100
    ClearActions(Myself)
    Continue()
END

IF
  !GlobalTimerNotExpired("DemTel","LOCALS")
  RandomNumLT(5,4)
  OR(5)
    See([PC.0.0.BARD_ALL])
    See([PC.0.0.DRUID_ALL])
    See([PC.0.0.SORCERER])
    See([PC.0.0.LONG_BOW])
    See([PC.0.0.CLERIC_ALL])
  !Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #90
    SetGlobalTimer("DemTel","LOCALS",18)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    DisplayStringHead(Myself,@8)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackReevaluate(LastSeenBy(Myself),180)
  RESPONSE #40
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  HPPercentLT(Myself,26)
  !GlobalTimerNotExpired("BPT","LOCALS")
  Range(NearestEnemyOf(Myself),4)
  OR(4)
    See(NearestMyGroupOfType([EVILCUTOFF]))
    See(ThirdNearestMyGroupOfType([EVILCUTOFF]))
    See(FifthNearestMyGroupOfType([EVILCUTOFF]))
    See(TenthNearestMyGroupOfType([EVILCUTOFF]))
  !Range(LastSeenBy(Myself),10)
THEN
  RESPONSE #90
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ForceSpell(LastSeenBy(Myself),WIZARD_DIMENSION_DOOR)
  RESPONSE #10
    Continue()
END

IF
  HitBy([GOODCUTOFF],CRUSHING)
  OR(2)
    !Range(LastHitter(Myself),7)
    !Range(LastAttackerOf(Myself),7)
  OR(2)
    See(LastHitter(Myself))
    See(LastAttackerOf(Myself))
  !TargetUnreachable(LastSeenBy(Myself))
  !GlobalTimerNotExpired("RANG","LOCALS")
THEN
  RESPONSE #90
    DisplayStringHead(Myself,@9)
    SetGlobalTimer("RANG","LOCALS",THREE_ROUNDS)
    SetGlobalTimer("DemTel","LOCALS",18)
    ActionOverride(LastSeenBy(Myself),ClearActions(Myself))
    ReallyForceSpell(LastSeenBy(Myself),DEMON_ATTRACTION)
    AttackReevaluate(LastSeenBy(Myself),180)
  RESPONSE #50
    DisplayStringHead(Myself,@10)
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
  RESPONSE #20
    DisplayStringHead(Myself,@11)
    SetGlobalTimer("RANG","LOCALS",ONE_ROUND)
  RESPONSE #50
    DisplayStringHead(Myself,@12)
    SetGlobalTimer("RANG","LOCALS",FIVE_ROUNDS)
    MoveToObject(LastSeenBy(Myself))
    Wait(3)
    AttackReevaluate(LastSeenBy(Myself),240)
END



IF
  OR(3)
    !Race(NearestEnemyOf(Myself),MORNINGSTAR)
    !Class(NearestEnemyOf(Myself),FAIRY_NYMPH)
    !Specifics(NearestEnemyOf(Myself),65)
  See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(28,22)
  OR(4)
    See([GOODCUTOFF.0.0.0.0.SUMMONED_DEMON])
    See([GOODCUTOFF.0.0.0.0.SUMMONED])
    See([GOODCUTOFF.0.0.0.SIMULACRUM])
    See([GOODCUTOFF.0.0.0.0.ILLUSIONARY])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(31,26)
  OR(6)
    See(NearestEnemyOf(Myself))
    See(WeakestOf)
    See(WorstAC)
    See(MostDamagedOf)
    See(LastAttackerOf(Myself))
    See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    Allegiance(LastSeenBy(Myself),ENEMY)
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODBUTRED)
THEN
  RESPONSE #100
END


IF
  Race(LastSeenBy(Myself),MORNINGSTAR)
  Class(LastSeenBy(Myself),FAIRY_NYMPH)
  Specifics(LastSeenBy(Myself),65)
THEN
  RESPONSE #100
END


IF
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
THEN
  RESPONSE #80
    Continue()
  RESPONSE #20
END



IF
  OR(8)
    General(LastSeenBy(Myself),UNDEAD)
    Race(LastSeenBy(Myself),DEMONIC)
    Race(LastSeenBy(Myself),RAKSHASA)
    Race(LastSeenBy(Myself),IMP)
    Race(LastSeenBy(Myself),MEPHIT)
    Gender(LastSeenBy(Myself),SUMMONED_DEMON)
    CheckStatGT(LastSeenBy(Myself),0,117)
    StateCheck(LastSeenBy(Myself),STATE_DEAD)
THEN
  RESPONSE #100
END

IF
  !GlobalTimerNotExpired("DemTel1","LOCALS")
  RandomNumLT(3,3)
  OR(6)
    Class(LastSeenBy(Myself),FIGHTER_ALL)
    Class(LastSeenBy(Myself),RANGER_ALL)
    Class(LastSeenBy(Myself),PALADIN_ALL)
    Class(LastSeenBy(Myself),THIEF_ALL)
    Class(LastSeenBy(Myself),MONK)
    HPGT(LastSeenBy(Myself),50)
  Range(LastSeenBy(Myself),10)
THEN
  RESPONSE #90
    SetGlobalTimer("DemTel1","LOCALS",THREE_ROUNDS)
    DisplayStringHead(Myself,314)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_REPULSION)
  RESPONSE #40
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END



IF
  RandomNumLT(26,18)
  OR(14)
    See([GOODCUTOFF.0.0.MONK])
    See([GOODCUTOFF.0.0.BARD_ALL])
    See([GOODCUTOFF.0.0.THIEF_ALL])
    See([GOODCUTOFF.0.0.RANGER_ALL])
    See([GOODCUTOFF.0.0.PALADIN_ALL])
    See([GOODCUTOFF.0.0.LONG_BOW])
    See([GOODCUTOFF.0.0.CLERIC_ALL])
    See([GOODCUTOFF.0.0.SORCERER])
    See([GOODCUTOFF.0.0.DRUID_ALL])
    See([GOODCUTOFF.0.0.CLERIC_ALL])
    See([GOODCUTOFF.0.0.CLERIC_MAGE])
    See([GOODCUTOFF.0.0.CLERIC_RANGER])
    See([GOODCUTOFF.0.0.PALADIN_ALL])
    See([GOODCUTOFF.0.0.CLERIC.0.0.MASK_GOOD])
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(29,23)
  OR(10)
    See(SecondNearestEnemyOfType([PC.0.0.RANGER_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.LONG_BOW]))
    See(SecondNearestEnemyOfType([PC.0.0.SORCERER]))
    See(SecondNearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(SecondNearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.RANGER_ALL]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.PALADIN_ALL]))
    See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC.0.0.MASK_GOOD]))
  RandomNumLT(27,23)
  OR(10)
    See(ThirdNearestEnemyOfType([PC.0.0.RANGER_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.LONG_BOW]))
    See(ThirdNearestEnemyOfType([PC.0.0.SORCERER]))
    See(ThirdNearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(ThirdNearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(ThirdNearestEnemyOfType([GOODCUTOFF.0.0.RANGER_ALL]))
    See(ThirdNearestEnemyOfType([GOODCUTOFF.0.0.PALADIN_ALL]))
    See(ThirdNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC.0.0.MASK_GOOD]))
  RandomNumLT(32,18)
  OR(7)
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.LONG_BOW]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.SORCERER]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.RANGER_ALL]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.PALADIN_ALL]))
    See(TenthNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC.0.0.MASK_GOOD]))
  RandomNumLT(20,8)
  See(TenthNearestEnemyOf(Myself))
  RandomNumLT(30,6)
  See(Protagonist)
  False()
THEN
  RESPONSE #100
    Continue()
END



IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(20,4)
  !StateCheck(LastSeenBy(Myself),STATE_BLIND)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",3)
    ReallyForceSpell(LastSeenBy(Myself),INNATE_IMPROVED_GLOBE_OF_DARKNESS)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,3)
  RandomNumLT(20,7)
  !StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  CheckStat(LastSeenBy(Myself),0,119)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",4)
    ReallyForceSpell(LastSeenBy(Myself),VAMPIRE_DOMINATION)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,6)
  RandomNumLT(20,9)
  NumCreatureVsPartyLT([ENEMY.UNDEAD],6)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    ReallyForceSpell(LastSeenBy(Myself),CLERIC_ANIMATE_DEAD)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,8)
  RandomNumLT(20,12)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  CheckStat(LastSeenBy(Myself),0,119)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",3)
    ReallyForceSpell(LastSeenBy(Myself),SPECTATOR_SUGGESTION)
END

IF
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumGT(20,11)
  RandomNumLT(20,15)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",4)
    ReallyForceSpell(LastSeenBy(Myself),DEMON_FEAR)
END

IF
  HaveSpell(CLERIC_SILENCE_15_FOOT)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(4,4)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
  !Range(LastSeenBy(Myself),15)
  !StateCheck(LastSeenBy(Myself),STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_SILENCE_15_FOOT)
END

IF
  HaveSpell(CLERIC_MISCAST_MAGIC)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(6,5)
  CheckStatLT(LastSeenBy(Myself),60,SPELLFAILUREMAGE)
  CheckStatLT(LastSeenBy(Myself),60,SPELLFAILUREPRIEST)
  OR(5)
    Class(LastSeenBy(Myself),DRUID_ALL)
    Class(LastSeenBy(Myself),BARD_ALL)
    Class(LastSeenBy(Myself),CLERIC_ALL)
    Class(LastSeenBy(Myself),LONG_BOW)
    Class(LastSeenBy(Myself),SORCERER)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  HaveSpell(CLERIC_CALL_LIGHTNING)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(7,5)
  AreaType(OUTDOOR)
  CheckStat(LastSeenBy(Myself),0,129)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_CALL_LIGHTNING)
END



IF
  RandomNumLT(24,9)
  OR(3)
    See(StrongestOf)
    See(BestAC)
    See(LeastDamagedOf)
  RandomNumLT(25,9)
  See(WeakestOf)
  RandomNumLT(23,8)
  See(WorstAC)
  RandomNumLT(21,8)
  See(MostDamagedOf)
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(32,25)
  OR(3)
    Heard(ThirdNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(SecondNearestMyGroupOfType([EVILCUTOFF]),124)
    Heard(NearestMyGroupOfType([EVILCUTOFF]),124)
  HPGT(LastHeardBy(Myself),1)
  Range(LastHeardBy(Myself),20)
  Exists(LastAttackerOf(LastHeardBy(Myself)))
  !Allegiance(LastAttackerOf(LastHeardBy(Myself)),EVILCUTOFF)
  !StateCheck(LastAttackerOf(LastHeardBy(Myself)),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(LastHeardBy(Myself)),MORNINGSTAR)
    !Class(LastAttackerOf(LastHeardBy(Myself)),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(LastHeardBy(Myself)),65)
  OR(3)  
    !StateCheck(LastAttackerOf(LastHeardBy(Myself)),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(LastHeardBy(Myself)))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumLT(33,13)
  HitBy([0],0x0000)
  !Allegiance(LastHitter(Myself),EVILCUTOFF)
  !StateCheck(LastHitter(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastHitter(Myself),MORNINGSTAR)
    !Class(LastHitter(Myself),FAIRY_NYMPH)
    !Specifics(LastHitter(Myself),65)
  OR(3)  
    !StateCheck(LastHitter(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastHitter(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  RandomNumGT(33,22)
  AttackedBy([0],DEFAULT)
  !Allegiance(LastAttackerOf(Myself),EVILCUTOFF)
  !StateCheck(LastAttackerOf(Myself),STATE_CHARMED)
  OR(3)
    !Race(LastAttackerOf(Myself),MORNINGSTAR)
    !Class(LastAttackerOf(Myself),FAIRY_NYMPH)
    !Specifics(LastAttackerOf(Myself),65)
  OR(3)  
    !StateCheck(LastAttackerOf(Myself),0x00400010)
    CheckStatGT(Myself,0,SEEINVISIBLE)
    CheckStatGT(Myself,0,118)
  See(LastAttackerOf(Myself))
  False()
THEN
  RESPONSE #100
    Continue()
END

IF
  OR(3)
    Allegiance(LastSeenBy(Myself),ENEMY)
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODBUTRED)
THEN
  RESPONSE #100
END


IF
  Race(LastSeenBy(Myself),MORNINGSTAR)
  Class(LastSeenBy(Myself),FAIRY_NYMPH)
  Specifics(LastSeenBy(Myself),65)
THEN
  RESPONSE #100
END


IF
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
THEN
  RESPONSE #80
    Continue()
  RESPONSE #20
END




IF
  HaveSpell(CLERIC_CAUSE_SERIOUS_WOUNDS)
  !GlobalTimerNotExpired("BPT","LOCALS")
  RandomNumLT(8,5)
  !HasItem("SERIOUS",Myself)
  CheckStat(LastSeenBy(Myself),0,181)
THEN
  RESPONSE #100
    SetGlobalTimer("BPT","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),CLERIC_CAUSE_SERIOUS_WOUNDS)
    AttackOneRound(LastSeenBy(Myself))
END

IF
  HPPercentLT(Myself,25)
  Range(LastHitter(Myself),10)
  !GlobalTimerNotExpired("BPW","LOCALS")
THEN
  RESPONSE #30
    SetGlobalTimer("BPW","LOCALS",10)
    DisplayStringHead(Myself,@13)
    Shout(124)
    RunAwayFrom(Nearest,90)
  RESPONSE #30
    SetGlobalTimer("BPW","LOCALS",10)
    DisplayStringHead(Myself,@14)
    Shout(124)
    EquipMostDamagingMelee()
    Attack(LastHitter(Myself))
  RESPONSE #40
    SetGlobalTimer("BPW","LOCALS",FOUR_ROUNDS)
    DisplayStringHead(Myself,314)
    ReallyForceSpell(NearestEnemyOf(Myself),DEMON_REPULSION)
    ReallyForceSpell(SecondNearestEnemyOf(Myself),DEMON_REPULSION)
    ReallyForceSpell(ThirdNearestEnemyOf(Myself),DEMON_REPULSION)
  RESPONSE #40
    SetGlobalTimer("BPW","LOCALS",16)
    DisplayStringHead(Myself,@15)
    Shout(123)
    ReallyForceSpell(LastHitter(Myself),CLERIC_ANIMATE_DEAD)
END

IF
  CheckStatGT(LastSeenBy(Myself),10,180)
  CheckStatLT(Myself,11,180)
THEN
  RESPONSE #60
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #90
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  OR(3)
    !Range(LastSeenBy(Myself),5)
    CheckStatGT(LastSeenBy(Myself),0,121)
    CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #60
    EquipRanged()
    AttackOneRound(LastSeenBy(Myself))
  RESPONSE #90
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  CheckStatGT(NearestEnemyOf(Myself),0,121)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
    ReallyForceSpell(NearestEnemyOf(Myself),DEMON_REPULSION)
END

IF
  CheckStatGT(LastSeenBy(Myself),0,125)
THEN
  RESPONSE #25
    Continue()
  RESPONSE #75
    ReallyForceSpell(LastSeenBy(Myself),DEMON_REPULSION)
END

IF
  !Range(LastSeenBy(Myself),8)
THEN
  RESPONSE #50
    EquipMostDamagingMelee()
    AttackReevaluate(NearestEnemyOf(Myself),45)
  RESPONSE #20
    EquipMostDamagingMelee()
    AttackReevaluate(LastSeenBy(Myself),75)
  RESPONSE #30
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
END

IF
  True()
THEN
  RESPONSE #60
    AttackReevaluate(LastSeenBy(Myself),45)
  RESPONSE #60
    ChangeSpecifics(Myself,CALLED_FOR_BOULDER)
    FaceObject(LastSeenBy(Myself))
  RESPONSE #45
END

